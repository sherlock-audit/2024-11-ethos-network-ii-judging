Flat Pear Owl

High

# Double counting of protocol and donation fees leads to potential protocol drain

### Summary

In `ReputationMarket.sol`, the protocol fails to properly handle the update of `marketFunds` in the `buyVotes` function. It double counts the protocol fee and donation fee. As a result, when someone attempts to withdraw graduated market funds, it could potentially drain the entire protocol.

### Root Cause

In `buyVotes`:
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L451
```solidity
    (
      uint256 votesBought,
      uint256 fundsPaid, //@audit
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
    applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
    marketFunds[profileId] += fundsPaid; //@audit
```

It calls `_calculateBuy` to calc`fundsPaid` which is the amount that user should pay. And it updates `marketFunds[profileId]`  using the following code `marketFunds[profileId] += fundsPaid;`. 

In function `_calculateBuy`:
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L978
```solidity 
  function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
    fundsPaid += protocolFee + donation; // @audit
    // ... 
    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }
```
It adds protocol fee and donation fee to fundsPaid. so actually the `fundsPaid` consists 3 parts: 
1. the amout used to buy votes
2. protocol fee
3. donation fee 

The protocol fee have been send to protocol fee receiver when call `applyFees` in `buyVotes` and the donation fee can also be withdraw through `withdrawDonations`. 
But in function `withdrawGraduatedMarketFunds` , it sends the whole`markFunds[profileId]` to msg.sender. 
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L675
```solidity
  function withdrawGraduatedMarketFunds(uint256 profileId) public whenNotPaused {
    address authorizedAddress = contractAddressManager.getContractAddressForName(
      "GRADUATION_WITHDRAWAL"
    );
    if (msg.sender != authorizedAddress) {
      revert UnauthorizedWithdrawal();
    }
    _checkMarketExists(profileId);
    if (!graduatedMarkets[profileId]) {
      revert MarketNotGraduated();
    }
    if (marketFunds[profileId] == 0) {
      revert InsufficientFunds();
    }

    _sendEth(marketFunds[profileId]); //@audit 
    emit MarketFundsWithdrawn(profileId, msg.sender, marketFunds[profileId]);
    marketFunds[profileId] = 0;
  }
```
As a result, the protocol can be drained due to the double counting of the protocol fee and donation fee.
It doesn't matter the caller of withdrawGraduatedMarketFunds is trusted, as the issue only requires them to call this function.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

potentially drain the entire protocol.

### PoC

_No response_

### Mitigation

remove protocol fee and donation fee from `fundsPaid`  after send refund 