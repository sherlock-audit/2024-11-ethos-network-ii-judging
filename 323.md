Custom Foggy Pike

Medium

# `ReputationMarket.buyVotes()` : protocol and donation fees are calculated based on the total sent tokens (`msg.value`) instead of calculating it based on the total price of the bought votes

### Summary

`ReputationMarket.buyVotes()` function charges buyer for protocol and donation fees based on the total sent tokens (`msg.value`) instead of the total price of the bought votes, which will result in users buying more fees for the protocol and donation.

### Root Cause

This is caused by the currently implemented mechanism to **calculate fees first** based on the sent tokens (`msg.value`), instead of calculating these fees based on the price of the bought votes.


### Internal pre-conditions

- `ReputationMarket.buyVotes()` enables users to buy votes from an active market, where they send their txn with a `msg.value`, then the protocol fees and donation fees are calculated as percentages from the sent `msg.value`, then the remaining amount is used to buy votes.

- The calculation of fees and number of bougth votes are handled by [`_calculateBuy()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L942):

```javascript
function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
    fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }
```

where:

```javascript
  function previewFees(
    uint256 amount,
    bool isEntry
  ) private view returns (uint256 funds, uint256 protocolFee, uint256 donation) {
    if (isEntry) {
      protocolFee = (amount * entryProtocolFeeBasisPoints) / BASIS_POINTS_BASE;
      donation = (amount * donationBasisPoints) / BASIS_POINTS_BASE;
    } else {
      protocolFee = (amount * exitProtocolFeeBasisPoints) / BASIS_POINTS_BASE;
    }
    funds = amount - protocolFee - donation;
  }
```

- As can be noticed, the protocol and donation fees are first deducted from the `msg.value`, then the remaining amount is used to buy votes, and if there's any remaining tokens that are not enogh to buy a vote; it will be [refunded](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L477C3-L478C38) to the buyer:

```javascript
 function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
    applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
    marketFunds[profileId] += fundsPaid;
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```


### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The current implemented mechanism charges fees on the total sent amount instead of charging it based on the number of bought votes, which will result in buyers paying more fees for the protocol and donation, and as a result of that they will **receive less votes than their actual tokens can get**.

### PoC

_No response_

### Mitigation

Update the current fees charging mechanism to charge the protocol fees and donation fees **based on the price of the bought votes** instead of calculating it based on the total sent `msg.value`.