Brilliant Jetblack Swan

High

# `ReputationMarket::buyVotes` function doesn't account `marketFunds` correctly

### Summary

When users buy votes of the specific market, `buyVotes` function accounts `marketFunds` incorrectly and this could make some graduated markets unable to withdraw their funds. Also this could affect withdrawal of donation due to the insufficient funds.

### Root Cause

`buyVotes` function doesn't account `marketFunds` correctly.
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481


### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

N/A

### Impact

When graduated markets withdraw their funds, they can withdraw more than they should and remaining graduated markets will be unable to withdraw their funds because there is not enough funds in the contract. Some of the donation recipients could be unable to withdraw their funds due to the lack of funds.

### PoC

When users buy votes, `ReputationMarket::_calculateBuy` function is called and returns `fundsPaid`.

```solidity
File: ReputationMarket.sol
942: function _calculateBuy(
943:     Market memory market,
944:     bool isPositive,
945:     uint256 funds
946:   )
947:     private
948:     view
949:     returns (
950:       uint256 votesBought,
951:       uint256 fundsPaid,
952:       uint256 newVotePrice,
953:       uint256 protocolFee,
954:       uint256 donation,
955:       uint256 minVotePrice,
956:       uint256 maxVotePrice
957:     )
958:   {
959:     uint256 fundsAvailable;
960:     (fundsAvailable, protocolFee, donation) = previewFees(funds, true);

970:     while (fundsAvailable >= votePrice) {
971:       fundsAvailable -= votePrice;
972:       fundsPaid += votePrice;
973:       votesBought++;
974: 
975:       market.votes[isPositive ? TRUST : DISTRUST] += 1;
976:       votePrice = _calcVotePrice(market, isPositive);
977:     }
978:     fundsPaid += protocolFee + donation; //@audit fundsPaid is added with protocol fee and the donation fee

983:   }

```
At [L978](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L978), the `fundsPaid` is added with `protocolFee` and `donation`.

`buyVotes` function handles `protocolFee` and `donation` by utilizing `applyFees` function at [L464](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L464).

```solidity
File: ReputationMarket.sol
442: function buyVotes(
443:     uint256 profileId,
444:     bool isPositive,
445:     uint256 expectedVotes,
446:     uint256 slippageBasisPoints
447:   ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
448:     _checkMarketExists(profileId);
449: 
450:     // Determine how many votes can be bought with the funds provided
451:     (
452:       uint256 votesBought,
453:       uint256 fundsPaid,
454:       ,
455:       uint256 protocolFee,
456:       uint256 donation,
457:       uint256 minVotePrice,
458:       uint256 maxVotePrice
459:     ) = _calculateBuy(markets[profileId], isPositive, msg.value);
 
463:     // Apply fees first
464:     applyFees(protocolFee, donation, profileId);

481:     marketFunds[profileId] += fundsPaid;

493:   }

```

`applyFees` function sends `protocolFee` to the `protocolFeeAddress` and updates the corresponding `donationEscrow` mapping by adding `donation`.

```solidity
File: ReputationMarket.sol
1116: function applyFees(
1117:     uint256 protocolFee,
1118:     uint256 donation,
1119:     uint256 marketOwnerProfileId
1120:   ) private returns (uint256 fees) {
1121:     donationEscrow[donationRecipient[marketOwnerProfileId]] += donation;
1122:     if (protocolFee > 0) {
1123:       (bool success, ) = protocolFeeAddress.call{ value: protocolFee }("");
1124:       if (!success) revert FeeTransferFailed("Protocol fee deposit failed");
1125:     }
1126:     fees = protocolFee + donation;
1127:   }

```
Hence `protocolFee` and `donation` should not be accounted as `marketFunds` as they belong to the protocol and the donation recipient. 

However, at [L481](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481), `buyVotes` function just adds `fundsPaid` which includes `protocolFee` and `donation` to the `marketFunds`. This results in `marketFunds` gets bigger than it should be and `Ether` balance of this contract will be smaller than the sum of all marketFunds and the donationEscrow. As a result, when graduated markets withdraw their funds, they withdraw more than they should leaving insufficient funds for remaining graduated markets and donation recipients.


### Mitigation

It is recommended to fix the `buyVotes` function as follows:

```diff

function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
    applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
-   marketFunds[profileId] += fundsPaid;
+   marketFunds[profileId] += fundsPaid - protocolFee - donation;
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }

```