Tall Cream Finch

Medium

# Users may pay more fees than they should when buying votes.

### Summary

In `ReputationMarket.sol:960`, when calculating the buying fees, the amount used is the input parameter `funds`, not the actual amount spent by the user (i.e. the `fundsPaid` in `ReputationMarket.sol:978`), which is less than the former, resulting in the user paying unnecessary additional fees.
```solidity
// function： ReputationMarket.sol:_calculateBuy(...)

        uint256 fundsAvailable;
960:    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
        uint256 votePrice = _calcVotePrice(market, isPositive);
    
        uint256 minPrice = votePrice;
        uint256 maxPrice;
    
        if (fundsAvailable < votePrice) {
          revert InsufficientFunds();
        }
    
970:    while (fundsAvailable >= votePrice) {
          fundsAvailable -= votePrice;
          fundsPaid += votePrice;
          votesBought++;
    
          market.votes[isPositive ? TRUST : DISTRUST] += 1;
          votePrice = _calcVotePrice(market, isPositive);
        }
978:    fundsPaid += protocolFee + donation;
    
        maxPrice = votePrice;
    
        return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
```
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L959-L982

In the `_calculateBuy` function, the fees are first calculated and deducted. The remaining funds (the `fundsAvailable` in `ReputationMarket.sol:960`) are used to pay for the prices of votes. This is to ensure that the actual ETH amount spent by the user does not exceed the input `funds`. 

According to `ReputationMarket.sol:970`, if after paying for some votes, `fundsAvailable > 0 && fundsAvailable < votePrice`, then the latest `fundsAvailable` will not be spent. As a result, the total funds spent by the user (the `fundsPaid` in `ReputationMarket.sol:978`) will be less than the initially input `funds`, because `fundsPaid = funds - fundsAvailable`. However, the fees have already been calculated based on the initially input `funds` and have not been updated after purchasing the votes, which leads to the user paying more fees than necessary.

### Root Cause

1. In `ReputationMarket.sol:960`, the total inputed `funds` are used to calculate the buying fees, instead of the actual spent funds, resulting in the user paying unnecessary additional fees.


### Internal pre-conditions

1. The input funds are not totally spent in `_calculateBuy`, i.e. in `ReputationMarket.sol:970`, `fundsAvailable > 0 && fundsAvailable < votePrice` after paying for some votes.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

This issue cause users to pay unnecessary additional fees, and the additional fees is non-trivial.

To show the potential fees loss, we assume that:
1. The total fee basis points are 200 (2%, protocol fee + donation), 
2. The latest `votePrice` becomes `0.02 ether` in `ReputationMarket.sol:970`. (The more votes are bought, the more `votePrice` will be.)
3. The latest `fundsAvailable` is `0.019 ether` in `ReputationMarket.sol:970`.
4. The fees loss to the user is `fundsAvailable * totalFeePercent = 0.019 * 2% = 0.00038 ether`.

In reality, the larger the lastest `fundsAvailable`, and the higher the fee basis points are, the greater the fee loss for the user.

### PoC

_No response_

### Mitigation

Use the total price of votes to update the fees after buying votes.
```solidity
// function： ReputationMarket.sol:_calculateBuy(...)
    uint256 fundsAvailable;
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
+   (fundsAvailable, protocolFee, donation) = previewFees(fundsPaid, true);
    fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
```