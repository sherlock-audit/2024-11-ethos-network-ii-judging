Sweet Carmine Dachshund

High

# Any ether withdrawing functions could be DoS'ed due to incorrect fund calculation in `ReputationMarket#buyVotes()`

### Summary

Anyone can purchase shares in a new reputation market using Ether. After deducting protocol fees and donation rewards, the remaining paid funds are deposited into the specified market:
```solidity
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
@>  applyFees(protocolFee, donation, profileId);//@audit-info deal with protocolFee and donation

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);//@audit-info unspent funds will return to buyer

    // tally market funds
@>  marketFunds[profileId] += fundsPaid;//@audit-info deposit fund into market
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```
However, `protocolFee` and `donation` was not deducted from `fundsPaid`, resulting higher value being added to `marketFunds[profileId]`. 
Once the market is graduated, its funds might not able to be withdrawn by calling `withdrawGraduatedMarketFunds()` due insufficient ether balance, or success withdrawing might DoS others from withdrawing ether from `ReputationMarket`.

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Any ether withdrawing functions could be DoS'ed due to insufficient ether balance in `ReputationMarket`:
- `sellVotes()`
- `withdrawDonations()`
- `withdrawGraduatedMarketFunds()`

### PoC

Copy below into [rep.graduate.test.ts](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/test/reputationMarket/rep.graduate.test.ts) and run `npm run test:contracts`:

```solidity
    it.only('should revert due to insufficient funds', async () => {
      
      await reputationMarket.connect(deployer.ADMIN).setProtocolFeeAddress(deployer.FEE_PROTOCOL_ACC);
      //@audit-info set entry protocol fee to 5%
      await reputationMarket.connect(deployer.ADMIN).setEntryProtocolFeeBasisPoints(500);
      expect(await reputationMarket.entryProtocolFeeBasisPoints()).to.equal(500);
      expect(await reputationMarket.donationBasisPoints()).to.equal(0);
      // Add funds through trading
      await userA.buyVotes({ buyAmount: ethers.parseEther('0.1') });
      // Graduate market
      await reputationMarket.connect(graduator).graduateMarket(DEFAULT.profileId);
      //@audit-info there is no enough ether in reputationMarket
      const funds = await reputationMarket.marketFunds(DEFAULT.profileId);
      const balance = await ethers.provider.getBalance(reputationMarket.getAddress());
      expect(balance).to.lessThan(funds);
      //@audit-info  Withdraw funds revert due to insufficient ether
      await expect(
        reputationMarket.connect(graduator).withdrawGraduatedMarketFunds(DEFAULT.profileId),
      ).to.be.revertedWith("ETH transfer failed");
    });
```

### Mitigation

Correct the [`ReputationMarket#buyVotes()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L442-L493):
```diff
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
    applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
-   marketFunds[profileId] += fundsPaid;
+   marketFunds[profileId] += fundsPaid - protocolFee - donation;
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```