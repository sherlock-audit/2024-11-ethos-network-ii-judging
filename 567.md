Sweet Carmine Dachshund

High

# Any ether withdrawing functions could be DoS'ed due to incorrect fund calculation in `ReputationMarket#sellVotes()`

### Summary

Anyone can sell their votes of an active market to get back ether:
```solidity
  function sellVotes(
    uint256 profileId,
    bool isPositive,
    uint256 amount
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // calculate the amount of votes to sell and the funds received
    (
      uint256 votesSold,
      uint256 fundsReceived,
      ,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

    // update the market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;

    // apply protocol fees
@>  applyFees(protocolFee, 0, profileId); //@audit-info pay protocol fee

    // send the proceeds to the seller
@>  _sendEth(fundsReceived);//@audit-info the rest will be transferred to the seller
    // tally market funds
    marketFunds[profileId] -= fundsReceived;
    emit VotesSold(
      profileId,
      msg.sender,
      isPositive,
      votesSold,
      fundsReceived,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```
After [pay the protocol fee](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L517), the remaining ether will [return to the seller](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L520).

However, the protocol fee was not deducted from the market:
```solidity
522:    marketFunds[profileId] -= fundsReceived;
```
Once the market is graduated, its funds might not able to be withdrawn due insufficient ether balance, or success withdrawing might DoS other functions from withdrawing ether from `ReputationMarket`.

### Root Cause

In [ReputationMarket.sol#L520](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L520), the protocol fee was not deducted from the market funds

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Any ether withdrawing functions could be DoS'ed due to insufficient ether balance in `ReputationMarket`:
- `sellVotes()`
- `withdrawDonations()`
- `withdrawGraduatedMarketFunds()`

### PoC

Copy below into [rep.graduate.test.ts](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/test/reputationMarket/rep.graduate.test.ts) and run `npm run test:contracts`:
```solidity
    it.only('should revert due to incorrect fund calculation in sellVotes', async () => {
      
      await reputationMarket.connect(deployer.ADMIN).setProtocolFeeAddress(deployer.FEE_PROTOCOL_ACC);
      //@audit-info buy votes
      await userA.buyVotes({ buyAmount: ethers.parseEther('0.1') });
      //@audit-info set exit protocol fee to 5%
      await reputationMarket.connect(deployer.ADMIN).setExitProtocolFeeBasisPoints(500);
      expect(await reputationMarket.exitProtocolFeeBasisPoints()).to.equal(500);
      expect(await reputationMarket.donationBasisPoints()).to.equal(0);
      //@audit-info sell votes
      const userAVotes = await reputationMarket.getUserVotes(
        userA.signer.address,
        DEFAULT.profileId,
      );
      await userA.sellVotes({ sellVotes: userAVotes.trustVotes });

      //@audit-info Graduate market
      await reputationMarket.connect(graduator).graduateMarket(DEFAULT.profileId);
      //@audit-info there is no enough ether in reputationMarket
      const funds = await reputationMarket.marketFunds(DEFAULT.profileId);
      const balance = await ethers.provider.getBalance(reputationMarket.getAddress());
      expect(balance).to.lessThan(funds);
      //@audit-info  Withdraw funds revert due to insufficient ether
      await expect(
        reputationMarket.connect(graduator).withdrawGraduatedMarketFunds(DEFAULT.profileId),
      ).to.be.revertedWith("ETH transfer failed");
    });
```


### Mitigation

The protocol fee should be deducted from the market funds when selling votes:
```diff
  function sellVotes(
    uint256 profileId,
    bool isPositive,
    uint256 amount
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // calculate the amount of votes to sell and the funds received
    (
      uint256 votesSold,
      uint256 fundsReceived,
      ,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

    // update the market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;

    // apply protocol fees
    applyFees(protocolFee, 0, profileId);

    // send the proceeds to the seller
    _sendEth(fundsReceived);
    // tally market funds
-   marketFunds[profileId] -= fundsReceived;
+   marketFunds[profileId] -= fundsReceived + protocolFee;
    emit VotesSold(
      profileId,
      msg.sender,
      isPositive,
      votesSold,
      fundsReceived,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```