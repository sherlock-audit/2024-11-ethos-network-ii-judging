Original Boysenberry Hare

High

# Incorrect Logic in `_calculateSell()` Function Causes Seller to Receive Fewer Funds Than Expected

## Description

**Context:**

Users holding trust or distrust votes purchased from a Reputation Market, can sell them to either profit from a price increase caused by other users buying the same type of vote or minimize losses by quickly exiting the market before the price drops further by selling at the current price.

**Vulnerability Details:**

The [_calculateSell()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1003-L1045) function is internally called when a user wants to [sell](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534) their trust/distrust votes.

If we examine the logic responsible for calculating the amount of funds the seller will receive:

[ReputationMarket.sol#L1031-L1040](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1031-L1040):

```solidity
    ...

    while (votesSold < amount) {
      if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
        revert InsufficientVotesToSell(profileId);
      }

      market.votes[isPositive ? TRUST : DISTRUST] -= 1;
      votePrice = _calcVotePrice(market, isPositive);
      fundsReceived += votePrice;
      votesSold++;
    }

    ...
```

1. First, the market total trust/distrust vote count is decremented by 1, indicating that a single trust/distrust vote has been sold (depending on which vote type the seller is selling):

```solidity
   market.votes[isPositive ? TRUST : DISTRUST] -= 1;
```

2. Next, the new vote price is calculated based on the updated market condition:

```solidity
    votePrice = _calcVotePrice(market, isPositive);
```

If we inspect the [_calcVotePrice()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L920-L923) function, it contains the following logic:

```solidity
  function _calcVotePrice(Market memory market, bool isPositive) private pure returns (uint256) {

    // 1. get total votes of the market (trust votes + distrust votes)
    uint256 totalVotes = market.votes[TRUST] + market.votes[DISTRUST];

    // 2. calculate vote price: number of trust/distrst votes market has * Market base Price / totalVotes
    return (market.votes[isPositive ? TRUST : DISTRUST] * market.basePrice) / totalVotes;

  }
```

3. The `fundsReceived` local variable is updated by incrementing it with the amount of funds the seller will receive in ETH:

```solidity
    fundsReceived += votePrice;
```

4. The loop continues until the number of votes the seller wants to sell is sold:

```solidity
    while (votesSold < amount) {
      ...


      ...
      votesSold++;
    }
```

The problem lies in the order of operations. First, the `fundsReceived` local variable should be updated to track the current vote price being sold. Next, the market condition should be updated, followed by recalculating the vote price based on the new market condition.

However, this is not how it is currently implemented. In the current implementation, the price used to update the `fundsReceived` variable reflects the condition after the market has already changed. As a result, the seller receives less funds than he should when selling his votes.

Take a look at the [_calculateBuy()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L942-L983) function, which is responsible for calculating how many votes a user can buy with the amount of ETH sent:

[ReputationMarket.sol#L961-L977](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L961-L977):

```solidity
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    ...

    ...

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
```

1. The user pays the current vote price based on the current market condition when buying a trust/distrust vote:

```solidity
   fundsAvailable -= votePrice;
```

2. The market total trust/distrust vote count is updated after payment:

```solidity
   market.votes[isPositive ? TRUST : DISTRUST] += 1;
```

3. The vote price is recalculated to reflect the updated market condition:

```solidity
   votePrice = _calcVotePrice(market, isPositive);
```

The main difference between `_calculateBuy()` and `_calculateSell()` functions, is the sequence of operations. Buyers pay the current vote price before the market condition is updated, while sellers receive funds after the market is updated. This discrepancy causes sellers to receive less than expected funds.

To resolve this issue, the `‍_calculateSell()‍‍‍‍‍` logic should match the `_calculateBuy()` logic or vice versa. Without this adjustment, sellers will continue to receive less than the expected funds.

## Impact

**Damage:** High

**Likelihood:** High

**Details:** Sellers receive fewer funds than they should when selling their votes. This means they will earn less if they are in profit, or if they are trying to minimize their losses, they will lose more funds.

## Proof of Concept

**Attack Path:**

1. Suppose a seller has `10 trust votes`, with each vote priced at `1 ETH`.
2. The seller sells all `10 trust votes`.
3. The seller receives less than `10 ETH`.

**POC:**

- Not Needed

## Recommended Mitigation

Refactor the `while` loop responsible for calculating the amount of funds the seller will receive inside the `_calculateSell()` function, as follows:

```diff
  function _calculateSell(
    Market memory market,
    uint256 profileId,
    bool isPositive,
    uint256 amount
  )
    private
    view
    returns (
      uint256 votesSold,
      uint256 fundsReceived,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    ...
        // some code here
    ...


    uint256 votePrice = _calcVotePrice(market, isPositive);


    ...
        // some code here
    ...


    while (votesSold < amount) {
      if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
        revert InsufficientVotesToSell(profileId);
      }
+     fundsReceived += votePrice;
      market.votes[isPositive ? TRUST : DISTRUST] -= 1;
      votePrice = _calcVotePrice(market, isPositive);
-     fundsReceived += votePrice;
      votesSold++;
    }

    ...
        // rest of the function
    ...
  }
```