Handsome Dijon Jay

Medium

# Lack of slippage check on `ReputationMarket::sellVotes` will cause vote sellers to sell at a worse price than intended

### Summary

The function [`ReputationMarket::buyVotes`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L435-L493) implements a [slippage check](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L461) to ensure that users do not receive fewer votes than they intended to purchase. This provides a safeguard against adverse pricing changes during the transaction and ensures predictable outcomes for the user. 

However, a similar protection is absent in the [`ReputationMarket::sellVotes`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534) function. Without a slippage check, users selling votes could potentially receive less compensation than expected due to changes in market conditions or other factors during execution, leaving them vulnerable to unfavorable outcomes. This inconsistency could result in unintended user losses.

### Root Cause

Lack of slippage check in [`ReputationMarket::sellVotes`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534)

### Internal pre-conditions

Another user needs to sell between the seller transmitted their tx and it is executed.

### External pre-conditions

_No response_

### Attack Path

1. Seller submits their tx to sell votes
2. Another users tx to sell votes is included first
3. Seller will sell at a disadvantageous price

### Impact

A lack of a slippage check in the `ReputationMarket::sellVotes` function means that users selling votes are exposed to the risk of receiving a lower price than anticipated.

### PoC

Foundry test:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import {Test} from "forge-std/Test.sol";

import { ERC1967Proxy } from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

import { ReputationMarket } from "../../contracts/ReputationMarket.sol";
import { IContractAddressManager } from "../../contracts/interfaces/IContractAddressManager.sol";
import { IEthosProfile } from "../../contracts/interfaces/IEthosProfile.sol";

contract ReputationMarketTest is Test {
    uint256 constant DEFAULT_PRICE = 0.01 ether;
    bool constant  TRUST = true;
    bool constant DISTRUST = false;

    ReputationMarket reputationMarket;

    address owner = makeAddr("owner");
    address admin = makeAddr("admin");
    address expectedSigner = makeAddr("expectedSigner");
    address signatureVerifier = makeAddr("signatureVerifier");
    address contractAddressManagerAddr = makeAddr("contractAddressManager");

    address profile = makeAddr("profile");

    address marketCreator = makeAddr("marketCreator");
    uint256 constant MarketCreatorProfile = 1;

    address alice = makeAddr("alice");
    address bob = makeAddr("bob");


    function setUp() public {
        ReputationMarket impl = new ReputationMarket();
        vm.label(address(impl), "impl");
        reputationMarket = ReputationMarket(address(new ERC1967Proxy(address(impl), "")));
        reputationMarket.initialize(
            owner,
            admin,
            expectedSigner,
            signatureVerifier,
            contractAddressManagerAddr
        );
        vm.label(address(reputationMarket), "ReputationMarket");

        vm.prank(admin);
        reputationMarket.setAllowListEnforcement(false);

        vm.mockCall(
            contractAddressManagerAddr,
            abi.encodeWithSelector(IContractAddressManager.getContractAddressForName.selector, "ETHOS_PROFILE"),
            abi.encode(profile)
        );

        vm.mockCall(
            profile,
            abi.encodeWithSelector(IEthosProfile.verifiedProfileIdForAddress.selector, marketCreator),
            abi.encode(MarketCreatorProfile)
        );
    }

    function testLackOfSlippageOnSell() public {
        vm.deal(marketCreator, 2 * DEFAULT_PRICE);
        vm.prank(marketCreator);
        reputationMarket.createMarket{value: 2 * DEFAULT_PRICE}();

        vm.deal(alice,1 ether);
        vm.deal(bob,1 ether);

        vm.prank(alice);
        reputationMarket.buyVotes{value: DEFAULT_PRICE/2}(MarketCreatorProfile, TRUST, 1, 0);

        vm.prank(bob);
        reputationMarket.buyVotes{value: (2* DEFAULT_PRICE)/3}(MarketCreatorProfile, TRUST, 1, 0);

        vm.prank(bob);
        ( , uint256 expectedSellPrice, , , , ) = reputationMarket.simulateSell(MarketCreatorProfile, TRUST, 1);

        // alice sells her vote first
        vm.prank(alice);
        reputationMarket.sellVotes(MarketCreatorProfile, TRUST, 1);

        uint256 balanceBefore = bob.balance;
        vm.prank(bob);
        reputationMarket.sellVotes(MarketCreatorProfile, TRUST, 1);

        // bob sold his vote for less than expected
        assertLt(bob.balance - balanceBefore, expectedSellPrice);
    }
}
```

### Mitigation

Consider implementing a slippage check on `ReputationMarket::sellVotes` where a user can supply an amount `minOut` or similar.