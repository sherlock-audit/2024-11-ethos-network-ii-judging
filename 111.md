Itchy Ginger Loris

High

# ReputationMarket contract can become insolvent due to wrong fee accounting


## Summary

Ethos Protocol implements fees that are applied on votes purchased and sold; however, these fees are not accounted for in the market liquidity. Such a logical error leads to the total liquidity amount of all markets plus donations recorded being higher than the `ReputationMarket` contract's balance, which will result in more funds being withdrawn after market graduation and, eventually, cause contract insolvency and reversal of withdrawal transactions.


## Vulnerability Detail

1. During a votes purchase call, the `_calculateBuy` function computes the number of votes bought, the funds paid for the votes, and the fee amounts. Subsequently, the fees are sent to the `protocolFeeAddress`, and the donation amount is accounted for accordingly. Finally, `marketFunds[profileId]` is increased by `fundsPaid`. However, the issue lies in the fact that `fundsPaid` represents the total gross amount paid by the buyer. As a result, this inflates the market balance (`marketFunds[profileId]`), which becomes greater than the actual contract balance increase.

```solidity
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
>>    uint256 fundsPaid,  // @audit gross amount: price for votesBought + protocolFee + donation
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
>>  applyFees(protocolFee, donation, profileId); // @audit fees paid, donation amount increased

    // ... irrelevant code skipped ...

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
>>  marketFunds[profileId] += fundsPaid; // @audit ISSUE: tally increased by gross amount!

    // ... irrelevant code skipped ...
  }
```

2. In contrast, during a votes sell call, the `_calculateSell` function returns the `fundsReceived` value as the net amount destined for the vote seller. This amount is then subtracted from `marketFunds[profileId]` ***without accounting for the fees*** sent to the `protocolFeeAddress`.

```solidity
  function sellVotes(
    uint256 profileId,
    bool isPositive,
    uint256 amount
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // calculate the amount of votes to sell and the funds received
    (
      uint256 votesSold,
>>    uint256 fundsReceived, // @audit net amount to be sewnt to the seller
      ,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

    // ... irrelevant code skipped ...

    // apply protocol fees
    applyFees(protocolFee, 0, profileId);

    // send the proceeds to the seller
    _sendEth(fundsReceived);
    // tally market funds
>>  marketFunds[profileId] -= fundsReceived; // @audit ISSUE: tally decreased without fees amount
    // ... irrelevant code skipped ...
  }
```


## Impact

More funds will be withdrawn after market graduation, which breaks the internal accounting and eventually leads to the reversal of sell and withdrawal transactions.

## Code snippets
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L442-L493

https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534


## Recommendation

Subtract fees from `marketFunds[profileId]` during buy and sell.
