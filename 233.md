Virtual Denim Bobcat

High

# All users lose a portion of their funds when selling their votes due to bad pricing logic

## Summary
Users lose funds when selling votes due to incorrect pricing order.

## Vulnerability Details
Users sell their votes by calling [ReputationMarket::sellVotes()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534), within this call, an internal function [_calculateSell()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1003-L1045) is used to calculate the amount of votes to sell including the funds received which will be sent to the calling user.

The problem lies within `_calculateSell()`, here's an extract:

```solidity
    ...
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 maxPrice = votePrice;
    uint256 minPrice;

    while (votesSold < amount) {
      if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
        revert InsufficientVotesToSell(profileId);
      }

      market.votes[isPositive ? TRUST : DISTRUST] -= 1;
      votePrice = _calcVotePrice(market, isPositive); //@audit: votePrice should be recalculated last after the next line, as the first vote price calculated before the loop should be used first 
      fundsReceived += votePrice;
      votesSold++;
    }
    (fundsReceived, protocolFee, ) = previewFees(fundsReceived, false);
    minPrice = votePrice;

    return (votesSold, fundsReceived, votePrice, protocolFee, minPrice, maxPrice);
  }
```

From the extract, the initial vote price is precalculated before the loop is entered and set as the `maxPrice`. Before this first value is used, it is recalculated after subtracting a vote which leads to a lower value for each vote being sold. Take the formula for vote prices:

```solidity
  function _calcVotePrice(Market memory market, bool isPositive) private pure returns (uint256) {
    uint256 totalVotes = market.votes[TRUST] + market.votes[DISTRUST];
    return (market.votes[isPositive ? TRUST : DISTRUST] * market.basePrice) / totalVotes;
  }
```

If there are 4 `TRUST` and `DISTRUST` votes each in a market with the `market.basePrice` set to `0.01 ETH` as it is done in configuration, the vote price for both `TRUST` and `DISTRUST` votes for that market would be `0.005 ETH` each since there are equal amounts of votes.

If a user wants to sell his 1 `TRUST` vote:

```text
votePrice = 0.005 ETH
First iteration:
    totalMarketTrustVotes -= 1
    new_vote_price = 0.00428571428 ETH
    fundsReceived += new_vote_price
```

Since it's a single vote, the loop only iterates once - but a loss of value is already shown, here the `fundsReceived` will be `0.00428 ETH` which is only `85.7%` of its correct value `0.005 ETH` - this represents a loss of almost `15%`, and with a higher number of votes being sold this value will worsen.

## Impact
Users lose a portion of their funds when selling votes due to incorrect pricing.

## Recommendation
Change the order of operations such that the precomputed vote pricing is utilized first:

```solidity
    ...
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 maxPrice = votePrice;
    uint256 minPrice;

    while (votesSold < amount) {
      if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
        revert InsufficientVotesToSell(profileId);
      }

      fundsReceived += votePrice;
      votesSold++;

      market.votes[isPositive ? TRUST : DISTRUST] -= 1;
      votePrice = _calcVotePrice(market, isPositive); //@audit-fix
    }
    (fundsReceived, protocolFee, ) = previewFees(fundsReceived, false);
    minPrice = votePrice;

    return (votesSold, fundsReceived, votePrice, protocolFee, minPrice, maxPrice);
  }
```

An example of where this is done correctly in the code is in [_calculateBuy()](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L970-L977).