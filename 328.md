Sweet Carmine Dachshund

Medium

# Ethos protocol may incur a loss of exit fees when `EthosVouch#unvouch()` is called

### Summary

The calculation of exit fee is incorrect, resulting the exit fee less than expected

### Links to affected code
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/EthosVouch.sol#L955

### Root Cause

When a user calls `EthosVouch#unvouch()`, the total amount of ETH they can withdraw should be calculated as below:
```math
\begin{align*}
fee_{exit} &= amount_{vouch} * bips_{exit} \\
amount_{withdraw} &= amount_{vouch} - fee_{exit} \\
&= amount_{vouch} * (1 - bips_{exit}) \\
\end{align*}
```
If the balance of the specified vouch is 100 ETH and $bips_{exit} = 0.03$, the owner of the vouch should pay `3 ETH` as exit fee and receive `97 ETH` when exiting the vouch.

However, the calculation of exit fee is incorrect:
```solidity
  function applyFees(
    uint256 amount,
    bool isEntry,
    uint256 subjectProfileId
  ) internal returns (uint256 toDeposit, uint256 totalFees) {
    if (isEntry) {
      // Calculate entry fees
      uint256 protocolFee = calcFee(amount, entryProtocolFeeBasisPoints);
      uint256 donationFee = calcFee(amount, entryDonationFeeBasisPoints);
      uint256 vouchersPoolFee = calcFee(amount, entryVouchersPoolFeeBasisPoints);

      // Distribute fees
      if (protocolFee > 0) {
        _depositProtocolFee(protocolFee);
      }
      if (donationFee > 0) {
        _depositRewards(donationFee, subjectProfileId);
      }
      if (vouchersPoolFee > 0) {
        // update the voucher pool fee to the amount actually distributed
        vouchersPoolFee = _rewardPreviousVouchers(vouchersPoolFee, subjectProfileId);
      }
      totalFees = protocolFee + donationFee + vouchersPoolFee;
      toDeposit = amount - totalFees;
    } else {
      // Calculate and apply exit fee
@>    uint256 exitFee = calcFee(amount, exitFeeBasisPoints);

      if (exitFee > 0) {
        _depositProtocolFee(exitFee);
      }
      totalFees = exitFee;
      toDeposit = amount - exitFee;
    }

    return (toDeposit, totalFees);
  }
```
```solidity
  function calcFee(uint256 total, uint256 feeBasisPoints) internal pure returns (uint256 fee) {
    /*
     * Formula derivation:
     * 1. total = deposit + fee
     * 2. fee = deposit * (feeBasisPoints/10000)
     * 3. total = deposit + deposit * (feeBasisPoints/10000)
     * 4. total = deposit * (1 + feeBasisPoints/10000)
     * 5. deposit = total / (1 + feeBasisPoints/10000)
     * 6. fee = total - deposit
     * 7. fee = total - (total * 10000 / (10000 + feeBasisPoints))
     */
    return
      total -
      (total.mulDiv(BASIS_POINT_SCALE, (BASIS_POINT_SCALE + feeBasisPoints), Math.Rounding.Floor));
  }
```
From above codes we can see, if a user exits a `100 ETH` vouch with $bips_{exit} = 0.03$, they'll pay a `2.91262 ETH` exit fee instead of `3 ETH`, resulting in a loss of protocol revenue:
```math
\begin{align*}
amount_{vouch} &= 100 ETH \\
bips_{exit} &= 3\% \\
fee_{exit} &= amount_{vouch} -  \frac{1}{1 + bips_{exit}} * amount_{vouch} \\
&= \frac{bips_{exit}}{1 + bips_{exit}} * amount_{vouch} \\
&= \frac{3\%}{1 + 3\%} * 100 ETH \\
&= 2.91262 ETH \\
\end{align*}
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

Ethos protocol may incur a loss of exit fees when `EthosVouch#unvouch()` is called

### PoC

_No response_

### Mitigation

The exit fee should be calculated as below:
```diff
  function applyFees(
    uint256 amount,
    bool isEntry,
    uint256 subjectProfileId
  ) internal returns (uint256 toDeposit, uint256 totalFees) {
    if (isEntry) {
      // Calculate entry fees
      uint256 protocolFee = calcFee(amount, entryProtocolFeeBasisPoints);
      uint256 donationFee = calcFee(amount, entryDonationFeeBasisPoints);
      uint256 vouchersPoolFee = calcFee(amount, entryVouchersPoolFeeBasisPoints);

      // Distribute fees
      if (protocolFee > 0) {
        _depositProtocolFee(protocolFee);
      }
      if (donationFee > 0) {
        _depositRewards(donationFee, subjectProfileId);
      }
      if (vouchersPoolFee > 0) {
        // update the voucher pool fee to the amount actually distributed
        vouchersPoolFee = _rewardPreviousVouchers(vouchersPoolFee, subjectProfileId);
      }
      totalFees = protocolFee + donationFee + vouchersPoolFee;
      toDeposit = amount - totalFees;
    } else {
      // Calculate and apply exit fee
-     uint256 exitFee = calcFee(amount, exitFeeBasisPoints);
+     uint256 exitFee = amount.mulDiv(exitFeeBasisPoints, BASIS_POINT_SCALE, Math.Rounding.Floor);

      if (exitFee > 0) {
        _depositProtocolFee(exitFee);
      }
      totalFees = exitFee;
      toDeposit = amount - exitFee;
    }

    return (toDeposit, totalFees);
  }
```