Zealous Golden Aardvark

Medium

# `isParticipant` is never set to false

### Summary

The [`isParticipant`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L123) mapping is used for checking if a user is a participant in a market or not.
```solidity
  // profileId => participant address
  // append only; don't bother removing. Use isParticipant to check if they've sold all their votes.
  mapping(uint256 => address[]) public participants;
```
A user is supposed to be determined as a participant when they buy votes using the `buyVotes` function.
```solidity
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    /// . . . Rest of the code . . .
    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }
    /// . . . Rest of the code . . .
}
```
However, the mapping should be updated to false when all votes are sold as per the code comments, which is missing inside the `sellVotes` function.
No kind of upgrade would fix such an issue.

### Root Cause

Missing `isParticipant` updation in the `sellVotes` function.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Anyone buys votes in an active market and sells all of them.
2. Any kind of contract upgrade would not fix such an issue as the mapping of it is eventually lost.

### Impact

1. Any future use case of `isParticipant` would be problematic as the mapping is lost.

### PoC

_No response_

### Mitigation

It is recommended to set `isParticipant` to false when all votes are sold in the `sellVotes` function.
```diff
function sellVotes(
    uint256 profileId,
    bool isPositive,
    uint256 amount
  ) public whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // calculate the amount of votes to sell and the funds received
    (
      uint256 votesSold,
      uint256 fundsReceived,
      ,
      uint256 protocolFee,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

    // update the market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] -= votesSold;
+     if(votesOwned[msg.sender][profileId].votes[TRUST] == 0 && votesOwned[msg.sender][profileId].votes[DISTRUST] == 0){
+          isParticipant[profileId][msg.sender] = false;
+     }
    // apply protocol fees
    applyFees(protocolFee, 0, profileId);

    // send the proceeds to the seller
    _sendEth(fundsReceived);
    // tally market funds
    marketFunds[profileId] -= fundsReceived;
    emit VotesSold(
      profileId,
      msg.sender,
      isPositive,
      votesSold,
      fundsReceived,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
  }
```
