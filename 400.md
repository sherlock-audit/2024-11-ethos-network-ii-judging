Handsome Dijon Jay

Medium

# Donation fees for mock profiles will be stuck in the `EthosVouch` contract

### Summary

In the Ethos ecosystem, profiles can have different statuses: `verified`, `archived`, and `mock`. While `mock` profiles cannot claim donation rewards, they can still be vouched for, which allows them to collect donation fees.

The issue arises because the donation fees collected by `mock` profiles are effectively stuck in the contract, as these profiles are not eligible to claim rewards. This creates a situation where the funds become inaccessible and potentially trapped within the system.

### Root Cause

In [`EthosVouch::applyFees`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/EthosVouch.sol#L929-L965), donation fees are applied, including to mock profiles. However, in [`EthosVouch::claimRewards`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/EthosVouch.sol#L667-L685), mock profiles are [ineligible](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/EthosVouch.sol#L673-L675) to claim these rewards. As a result, the collected fees remain stuck in the contract.

### Internal pre-conditions

Requires admin to have enabled donation fees

### External pre-conditions

_No response_

### Attack Path

1. User vouches for a mock profile
2. Mock profile owner cannot claim donation fee

### Impact

The donations fees paid to mock profiles will be stuck in the vouch contract.

### PoC

Foundry test:
```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.26;

import {Test} from "forge-std/Test.sol";

import { ERC1967Proxy } from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

import { EthosVouch } from "../../contracts/EthosVouch.sol";
import { IContractAddressManager } from "../../contracts/interfaces/IContractAddressManager.sol";
import { IEthosProfile } from "../../contracts/interfaces/IEthosProfile.sol";
import { ProfileNotFoundForAddress } from "../../contracts/errors/ProfileErrors.sol";

contract EthosVouchTest is Test {

    EthosVouch ethosVouch;

    address owner = makeAddr("owner");
    address admin = makeAddr("admin");
    address expectedSigner = makeAddr("expectedSigner");
    address signatureVerifier = makeAddr("signatureVerifier");
    address contractAddressManagerAddr = makeAddr("contractAddressManager");
    address feeProtocol = makeAddr("feeProtocol");

    address profile = makeAddr("profile");

    address subject = makeAddr("subject");
    uint256 constant SubjectProfileId = 1;

    address author = makeAddr("author");
    uint256 constant AuthorProfileId = 2;

    function setUp() public {
        EthosVouch impl = new EthosVouch();
        vm.label(address(impl), "impl");
        ethosVouch = EthosVouch(address(new ERC1967Proxy(address(impl), "")));
        ethosVouch.initialize(
            owner,
            admin,
            expectedSigner,
            signatureVerifier,
            contractAddressManagerAddr,
            feeProtocol,
            0, // entryProtocolFeeBasisPoints,
            500, // entryDonationFeeBasisPoints,
            0, // entryVouchersPoolFeeBasisPoints,
            0 // exitFeeBasisPoints
        );
        vm.label(address(ethosVouch), "EthosVouch");

        vm.mockCall(
            contractAddressManagerAddr,
            abi.encodeWithSelector(IContractAddressManager.getContractAddressForName.selector, "ETHOS_PROFILE"),
            abi.encode(profile)
        );

        vm.mockCall(
            profile,
            abi.encodeWithSelector(IEthosProfile.verifiedProfileIdForAddress.selector, author),
            abi.encode(AuthorProfileId)
        );

        vm.deal(author, 0.01 ether);
    }

    function testVouchRewardsStuckInContractForMockProfiles() public {
        vm.mockCall(
            profile,
            abi.encodeWithSelector(IEthosProfile.profileStatusById.selector, SubjectProfileId),
            abi.encode(
                true,  // is verified
                false, // not archived
                true   // is mock
            )
        );

        vm.mockCall(
            profile,
            abi.encodeWithSelector(IEthosProfile.profileStatusByAddress.selector, subject),
            abi.encode(
                true,  // is verified
                false, // not archived
                true,  // is mock
                SubjectProfileId
            )
        );

        // author vouches and unvouches, only rewards should be left in the contract
        vm.startPrank(author);
        ethosVouch.vouchByProfileId{value: 0.01 ether}(SubjectProfileId, "comment", "metadata");
        ethosVouch.unvouch(0);
        vm.stopPrank();
        assertGt(author.balance, 0);

        // subject has rewards to claim
        assertGt(ethosVouch.rewards(SubjectProfileId), 0);

        // but cannot claim since the profile is a mock
        vm.prank(subject);
        vm.expectRevert(abi.encodeWithSelector(ProfileNotFoundForAddress.selector, subject));
        ethosVouch.claimRewards();

        // reward eth stuck in the vouch contract
        assertGt(address(ethosVouch).balance, 0);
        assertEq(address(ethosVouch).balance + author.balance, 0.01 ether);
    }
}
```

### Mitigation

Consider either not collecting the donation fee for mock profiles or to let the mock profile collect it.