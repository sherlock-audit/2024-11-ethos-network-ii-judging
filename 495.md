Proud Chartreuse Whale

Medium

# Transfer of ownership of address can cause one Profile unvouch for another Profile

### Summary

With the current implementation ,if the ownership of address with which a profile vouched gets transferred to another Profile, then the later profile can unvouch the vouches of the first profile and get the funds of the first profile as the user wont be able to change the `authorAddress` of a vouch

### Root Cause

In EthosVouch.vouchByProfileId() , when a particular profile vouches, the authorAddress is set and after that it cannot be changed. And if the ownership of the address changes to another profile , then with the current implementation, user will never be able to unvouch and this also leads to another profile unvouching this profile's vouch.

```Solidity
  function vouchByProfileId(
    uint256 subjectProfileId,
    string calldata comment,
    string calldata metadata
  ) public payable whenNotPaused nonReentrant {
    // validate author profile
    uint256 authorProfileId = IEthosProfile(
      contractAddressManager.getContractAddressForName(ETHOS_PROFILE)
    ).verifiedProfileIdForAddress(msg.sender);

    // pls no vouch for yourself
    if (authorProfileId == subjectProfileId) {
      revert SelfVouch(authorProfileId, subjectProfileId);
    }

    // users can't exceed the maximum number of vouches
    if (vouchIdsByAuthor[authorProfileId].length >= maximumVouches) {
      revert MaximumVouchesExceeded(
        vouchIdsByAuthor[authorProfileId].length,
        "Exceeds author vouch limit"
      );
    }

    // validate subject profile
    if (subjectProfileId == 0) {
      revert InvalidEthosProfileForVouch(subjectProfileId);
    }
    (bool verified, bool archived, bool mock) = IEthosProfile(
      contractAddressManager.getContractAddressForName(ETHOS_PROFILE)
    ).profileStatusById(subjectProfileId);

    // you may not vouch for archived profiles
    // however, you may vouch for verified AND mock profiles
    // we allow vouching for mock profiles in case they are later verified
    if (archived || (!mock && !verified)) {
      revert InvalidEthosProfileForVouch(subjectProfileId);
    }

    // one vouch per profile per author
    _vouchShouldNotExistFor(authorProfileId, subjectProfileId);

    // don't exceed maximum vouches per subject profile
    if (vouchIdsForSubjectProfileId[subjectProfileId].length >= maximumVouches) {
      revert MaximumVouchesExceeded(
        vouchIdsForSubjectProfileId[subjectProfileId].length,
        "Exceeds subject vouch limit"
      );
    }

    // must meet the minimum vouch amount
    if (msg.value < configuredMinimumVouchAmount) {
      revert MinimumVouchAmount(configuredMinimumVouchAmount);
    }

    (uint256 toDeposit, ) = applyFees(msg.value, true, subjectProfileId);

    // store vouch details
    uint256 count = vouchCount;
    vouchIdsByAuthor[authorProfileId].push(count);
    vouchIdsByAuthorIndex[authorProfileId][count] = vouchIdsByAuthor[authorProfileId].length - 1;
    vouchIdsForSubjectProfileId[subjectProfileId].push(count);
    vouchIdsForSubjectProfileIdIndex[subjectProfileId][count] =
      vouchIdsForSubjectProfileId[subjectProfileId].length -
      1;

    vouchIdByAuthorForSubjectProfileId[authorProfileId][subjectProfileId] = count;
    vouches[count] = Vouch({
      archived: false,
      unhealthy: false,
      authorProfileId: authorProfileId,
      authorAddress: msg.sender,
      vouchId: count,
      balance: toDeposit,
      subjectProfileId: subjectProfileId,
      comment: comment,
      metadata: metadata,
      activityCheckpoints: ActivityCheckpoints({
        vouchedAt: block.timestamp,
        unvouchedAt: 0,
        unhealthyAt: 0
      })
    });

    emit Vouched(count, authorProfileId, subjectProfileId, msg.value);
    vouchCount++;
  }

```
```Solidity
  function increaseVouch(uint256 vouchId) public payable nonReentrant {
    // vouch increases much also meet the minimum vouch amount
    if (msg.value < configuredMinimumVouchAmount) {
      revert MinimumVouchAmount(configuredMinimumVouchAmount);
    }
    // get the profile id of the author
    uint256 profileId = IEthosProfile(
      contractAddressManager.getContractAddressForName(ETHOS_PROFILE)
    ).verifiedProfileIdForAddress(msg.sender);
    _vouchShouldBelongToAuthor(vouchId, profileId);
    // make sure this vouch is active; not unvouched
    _vouchShouldBePossibleUnvouch(vouchId);

    uint256 subjectProfileId = vouches[vouchId].subjectProfileId;
    (uint256 toDeposit, ) = applyFees(msg.value, true, subjectProfileId);
    vouches[vouchId].balance += toDeposit;

    emit VouchIncreased(vouchId, profileId, subjectProfileId, msg.value);
  }
```

```Solidity
  // --- Unvouch Functions ---

  /**
   * @dev Unvouches vouch.
   * @param vouchId Vouch Id.
   */
  function unvouch(uint256 vouchId) public whenNotPaused nonReentrant {
    Vouch storage v = vouches[vouchId];
    _vouchShouldExist(vouchId);
    _vouchShouldBePossibleUnvouch(vouchId);
    // because it's $$$, you can only withdraw/unvouch to the same address you used to vouch
    // however, we don't care about the status of the address's profile; funds are always attached
    // to an address, not a profile
    if (vouches[vouchId].authorAddress != msg.sender) {
      revert AddressNotVouchAuthor(vouchId, msg.sender, vouches[vouchId].authorAddress);
    }

    v.archived = true;
    // solhint-disable-next-line not-rely-on-time
    v.activityCheckpoints.unvouchedAt = block.timestamp;
    // remove the vouch from the tracking arrays and index mappings
    _removeVouchFromArrays(v);

    // apply fees and determine how much is left to send back to the author
    (uint256 toWithdraw, ) = applyFees(v.balance, false, v.subjectProfileId);
    // set the balance to 0 and save back to storage
    v.balance = 0;
    // send the funds to the author
    // note: it sends it to the same address that vouched; not the one that called unvouch
    (bool success, ) = payable(v.authorAddress).call{ value: toWithdraw }("");
    if (!success) {
      revert FeeTransferFailed("Failed to send ETH to author");
    }

    emit Unvouched(v.vouchId, v.authorProfileId, v.subjectProfileId);
  }
```
[RepoLink](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/EthosVouch.sol#L330-L481)

This needs to be handled especially because **the increaseVouch() function lets other addresses of the first profile also to increase the particular vouch amount** and later if the initial vouched address ownership is changed then the user can never unvouch and this also lets another profile unvouch the first profile's vouch and take up the entire vouch amount added by the first user


### Attack Path

Say Alice has 3 addresses : addrA,addrB,addrC

Alice vouches a profile say Alex using addrA  say amount is 1 ETH
Then later Alice decides to increase vouch but this time user increases vouch from addrB and increases vouch to 5 ETH
And later increases vouch to 7 ETH from addrC

And then later the ownership of addrA changed to another profile Bob(Alice deletes addrA and Bob registers addrA)

Then the Alice can still increase the vouch using addrB and addrC but Alice will never be able to unvouch and also Bob will be able to unvouch and take up all the funds of Alice


### Impact

Since User wont be able to change the authorAddress of a vouch and also since any address of the user can increase the vouch ,during transfer of the initial vouch address, the user will never be able to unvouch and another user will unvouch and take up the funds of the first user.



### Mitigation

Implement a function in which the users can change the authorAddress of a particular vouch so that during the transfer of ownership of the initial address ,the user can change the authorAddress to another address of the same profile so that user dont lose control over the vouch

Also implement a check in unvouch() to check if the vouches[vouchId].authorProfileId and the profileId of msg.sender matches 