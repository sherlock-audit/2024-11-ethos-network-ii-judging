Passive Tawny Sheep

Medium

# The buyVotes function charge fees for votes that the user didn't buy

### Summary

The function`buyVotes:442` charge fees for all the msg.value before calculating the used funds to buy votes meaning that the protocol will charge fees even with the rest of the Eth amount event if it's not used.

### Root Cause

In the `buyVotes` function the `_calculateBuy` function is called with the msg.value as argument as we can see here :
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L459
The function charge fees directly before calculating the amount to use as we can see here : 
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L942-L960
This means that fees will be charged even for funds that are not used in the purchase transaction. The user could, for the same votes purchased, pay more fees, which is unfair, the protocol should charge a fee corresponding to the amount purchased by the user.

### Internal pre-conditions

none.

### External pre-conditions

none.

### Attack Path

_No response_

### Impact

The user can potentially pay more fees than he should or get less votes that he can expect.

### PoC

In order to run this POC you will to add foundry to the project by follow simple steps : 
1.  run `npm install --save-dev @nomicfoundation/hardhat-foundry`
2. add in the hadhat config `import "@nomicfoundation/hardhat-foundry";` 
3. run `npx hardhat init-foundry`
4. create a remappings.txt file in the contracts folder and add those lines :
```solidity
@openzeppelin/contracts/=node_modules/@openzeppelin/contracts/
@openzeppelin/contracts-upgradeable/=node_modules/@openzeppelin/contracts-upgradeable/
```
5. Move the node_modules folder in the contracts folder.

Now you can copy paste this code in the test folder and run `forge test --mt test_simulateBuyPOC --via-ir`
```solidity 
// SPDX-License-Identifier: GPL-2.0
pragma solidity ^0.8.0;

import {Test, console2} from "forge-std/Test.sol";

import {EthosVouch} from "contracts/EthosVouch.sol";
import {ReputationMarket} from "contracts/ReputationMarket.sol";
import {EthosAttestation} from "contracts/EthosAttestation.sol";
import {EthosProfile} from "contracts/EthosProfile.sol";
import {ContractAddressManager} from "contracts/utils/ContractAddressManager.sol";
import {SignatureVerifier} from "contracts/utils/SignatureVerifier.sol";
import {InteractionControl} from "contracts/utils/InteractionControl.sol";
import {EthosReview} from "contracts/EthosReview.sol";
import {EthosVote} from "contracts/EthosVote.sol";
import {RejectETHReceiver} from "contracts/mocks/RejectETH.sol";
import {PaymentToken} from "contracts/mocks/PaymentToken.sol";
import {TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";

contract CodedPOC is Test {
    event LogBytes32(string message, bytes32 value);
    event LogBytes(string message, bytes value);
    event LogUint256(string message, uint256 value);
    event LogUint256Array(string message, uint256[] value);
    event LogAddressArray(string message, address[] value);
    event LogAddress(string message, address value);
    event LogBool(string message, bool value);

    EthosVouch ethosVouch;
    ContractAddressManager contractAddressManager;
    ReputationMarket reputationMarket;
    SignatureVerifier signatureVerifier;
    InteractionControl interactionControl;
    EthosAttestation ethosAttestation;
    EthosProfile ethosProfile;
    EthosReview ethosReview;
    EthosVote ethosVote;
    RejectETHReceiver rejectETHReceiver;
    address constant BOB = address(0x10000);
    address constant ALICE = address(0x20000);
    address constant CHARLIE = address(0x30000);
    address constant OWNER = address(0x40000);
    address constant ADMIN = address(0x50000);
    address constant expectedSigner = address(0x60000);
    address constant feeProtocolAddr = address(0x70000);
    address constant proxyAdmin = address(0x80000);
    address constant slashing = address(0x80000);
    address constant graduateAddress = address(0x90000);

    address sender;
    address[] internal users;

    string constant attestation = "ETHOS_ATTESTATION";
    string constant contractAddressManagerName = "ETHOS_CONTRACT_ADDRESS_MANAGER";
    string constant discussion = "ETHOS_DISCUSSION";
    string constant interactionControlName = "ETHOS_INTERACTION_CONTROL";
    string constant profil = "ETHOS_PROFILE";
    string constant reputationMarketName = "ETHOS_REPUTATION_MARKET";
    string constant review = "ETHOS_REVIEW";
    string constant signatureVerifierName = "ETHOS_SIGNATURE_VERIFIER";
    string constant vote = "ETHOS_VOTE";
    string constant vouch = "ETHOS_VOUCH";
    string constant vaultManager = "ETHOS_VAULT_MANAGER";
    string constant slashPenalty = "ETHOS_SLASH_PENALTY";
    string constant slasher = "SLASHER";
    string constant graduate = "GRADUATION_WITHDRAWAL";
    uint256 constant MAX_ETH = 100e18;
    PaymentToken paymentToken1;
    PaymentToken paymentToken2;
    uint256[] vouchIds;
    uint256[] vouchIdsActive;
    uint256[] vouchIdsArchived;
    uint256[] marketIds;
    uint256[] marketIdsActive;
    uint256[] marketIdsGraduated;
    uint256 subjectId;
    uint256 totalDepositVouch;
    mapping(address => uint256) profilIdBySender;
    uint256 constant TRUST = 1;
    uint256 constant DISTRUST = 0;

    function setUp() public {
        vm.warp(1524785992);
        vm.roll(4370000);
        users = [BOB, ALICE, CHARLIE];
        contractAddressManager = new ContractAddressManager();
        signatureVerifier = new SignatureVerifier();
        interactionControl = new InteractionControl(OWNER, address(contractAddressManager));

        EthosAttestation ethosAttestationimpl = new EthosAttestation();
        TransparentUpgradeableProxy ethosAttestationProxy =
            new TransparentUpgradeableProxy(address(ethosAttestationimpl), proxyAdmin, "");
        ethosAttestation = EthosAttestation(address(ethosAttestationProxy));
        ethosAttestation.initialize(
            OWNER, ADMIN, expectedSigner, address(signatureVerifier), address(contractAddressManager)
        );
        EthosProfile ethosProfileimpl = new EthosProfile();
        TransparentUpgradeableProxy ethosProfileProxy =
            new TransparentUpgradeableProxy(address(ethosProfileimpl), proxyAdmin, "");
        ethosProfile = EthosProfile(address(ethosProfileProxy));
        ethosProfile.initialize(
            OWNER, ADMIN, expectedSigner, address(signatureVerifier), address(contractAddressManager)
        );
        EthosReview ethosReviewimpl = new EthosReview();
        TransparentUpgradeableProxy ethosReviewProxy =
            new TransparentUpgradeableProxy(address(ethosReviewimpl), proxyAdmin, "");
        ethosReview = EthosReview(address(ethosReviewProxy));
        ethosReview.initialize(
            OWNER, ADMIN, expectedSigner, address(signatureVerifier), address(contractAddressManager)
        );
        EthosVote ethosVoteimpl = new EthosVote();
        TransparentUpgradeableProxy ethosVoteProxy =
            new TransparentUpgradeableProxy(address(ethosVoteimpl), proxyAdmin, "");
        ethosVote = EthosVote(address(ethosVoteProxy));
        ethosVote.initialize(OWNER, ADMIN, expectedSigner, address(signatureVerifier), address(contractAddressManager));
        EthosVouch ethosVouchimpl = new EthosVouch();
        TransparentUpgradeableProxy ethosVouchProxy =
            new TransparentUpgradeableProxy(address(ethosVouchimpl), proxyAdmin, "");
        ethosVouch = EthosVouch(address(ethosVouchProxy));
        ethosVouch.initialize(
            OWNER,
            ADMIN,
            expectedSigner,
            address(signatureVerifier),
            address(contractAddressManager),
            feeProtocolAddr,
            0,
            0,
            500,
            0
        );
        rejectETHReceiver = new RejectETHReceiver();
        address[] memory addresses = new address[](8);
        addresses[0] = address(ethosAttestation);
        addresses[1] = address(ethosProfile);
        addresses[2] = address(ethosReview);
        addresses[3] = address(ethosVote);
        addresses[4] = address(ethosVouch);
        addresses[5] = address(interactionControl);
        addresses[6] = address(slashing);
        addresses[7] = address(graduateAddress);
        string[] memory names = new string[](8);
        names[0] = attestation;
        names[1] = profil;
        names[2] = review;
        names[3] = vote;
        names[4] = vouch;
        names[5] = interactionControlName;
        names[6] = slasher;
        names[7] = graduate;
        contractAddressManager.updateContractAddressesForNames(addresses, names);
        string[] memory namesInteraction = new string[](5);
        namesInteraction[0] = attestation;
        namesInteraction[1] = profil;
        namesInteraction[2] = review;
        namesInteraction[3] = vote;
        namesInteraction[4] = vouch;
        vm.prank(OWNER);
        interactionControl.addControlledContractNames(namesInteraction);
        paymentToken1 = new PaymentToken("PAYMENT TOKEN NAME 1", "PTN 1");
        paymentToken2 = new PaymentToken("PAYMENT TOKEN NAME 2", "PTN 2");
        for (uint256 i = 0; i < users.length; i++) {
            paymentToken1.mint(users[i], 1_000_000e18);
            paymentToken2.mint(users[i], 1_000_000e18);
            vm.prank(OWNER);
            ethosProfile.inviteAddress(users[i]);
            vm.prank(users[i]);
            paymentToken1.approve(address(ethosVouch), type(uint256).max);
            vm.prank(users[i]);
            paymentToken2.approve(address(ethosVouch), type(uint256).max);
            vm.prank(users[i]);
            ethosProfile.createProfile(1);
            vm.deal(address(users[i]), 100_000_000e18);
        }
        ReputationMarket reputationMarketimpl = new ReputationMarket();
        TransparentUpgradeableProxy reputationMarketProxy =
            new TransparentUpgradeableProxy(address(reputationMarketimpl), proxyAdmin, "");
        reputationMarket = ReputationMarket(address(reputationMarketProxy));
        reputationMarket.initialize(
            OWNER, ADMIN, expectedSigner, address(signatureVerifier), address(contractAddressManager)
        );
        profilIdBySender[BOB] = ethosProfile.profileIdByAddress(BOB);
        profilIdBySender[ALICE] = ethosProfile.profileIdByAddress(ALICE);
        profilIdBySender[CHARLIE] = ethosProfile.profileIdByAddress(CHARLIE);
        subjectId = profilIdBySender[BOB];
        vm.label(BOB, "BOB");
        vm.label(ALICE, "ALICE");
        vm.label(CHARLIE, "CHARLIE");
        vm.label(OWNER, "OWNER");
        vm.label(ADMIN, "ADMIN");
        vm.label(expectedSigner, "expectedSigner");
        vm.label(feeProtocolAddr, "feeProtocolAddr");
        vm.label(slashing, "slashing");
        vm.label(graduateAddress, "graduateAddress");
        vm.label(address(ethosAttestation), "ethosAttestation");
        vm.label(address(ethosProfile), "ethosProfile");
        vm.label(address(ethosReview), "ethosReview");
        vm.label(address(ethosVote), "ethosVote");
        vm.label(address(ethosVouch), "ethosVouch");
        vm.label(address(interactionControl), "interactionControl");
        vm.label(address(reputationMarket), "reputationMarket");
        vm.label(address(contractAddressManager), "contractAddressManager");
        vm.label(address(signatureVerifier), "signatureVerifier");
        vm.label(address(rejectETHReceiver), "rejectETHReceiver");
        vm.label(address(paymentToken1), "paymentToken1");
        vm.label(address(paymentToken2), "paymentToken2");
        vm.prank(ADMIN);
        reputationMarket.setAllowListEnforcement(false);
        vm.prank(ADMIN);
        reputationMarket.setProtocolFeeAddress(feeProtocolAddr);
    }
   function test_simulateBuyPOC() public {
        // we start by set the entry protocol fee to 5% basis points
         vm.prank(ADMIN);
        reputationMarket.setEntryProtocolFeeBasisPoints(500);
        //Bob create a premium tier market
        vm.prank(BOB);
        reputationMarket.createMarketWithConfig{value: 1000000000000000000}(2); 
        //We use the simulateBuy function to simulate the purchase with 5 ether
       ( uint256 votesBought1,,,uint256 protocolFees1,,,) = reputationMarket.simulateBuy(2, false, 5 ether);
        //We use the simulateBuy function to simulate the purchase with 5.001 ether
       ( uint256 votesBought2,,,uint256 protocolFees2,,,) = reputationMarket.simulateBuy(2, false, 5.001 ether);
        //we receive the same amount of votes
         assertEq(votesBought1,votesBought2, "");
        //But we paid a highest fee which make no sense and is unfair
         assertGt(protocolFees2,protocolFees1, "");
    }
}
```
You should have this output meaning that the user paid more fees with the same votes: 
```solidity 
[PASS] test_simulateBuyPOC() (gas: 1929917)
```

### Mitigation

The protocol should compute the fees after using it for the vote amount : 

```solidity 
function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable = funds;
  
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
   (fundsPaid, protocolFee, donation) = previewFees(fundsPaid, true);
    fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }

```