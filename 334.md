Sweet Carmine Dachshund

Medium

# The author might be unable to mark their unvouched vouch as `unhealthy` in time if `unhealthyResponsePeriod` is updated suddenly

### Links to affected code
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/EthosVouch.sol#L857-L858

### Summary

When a vouch is un-vouched, its author could mark it as unhealthy as long as `unhealthyResponsePeriod` is not expired:
```solidity
  function markUnhealthy(uint256 vouchId) public whenNotPaused {
    Vouch storage v = vouches[vouchId];
    uint256 profileId = IEthosProfile(
      contractAddressManager.getContractAddressForName(ETHOS_PROFILE)
    ).verifiedProfileIdForAddress(msg.sender);

    _vouchShouldExist(vouchId);
@>  _vouchShouldBePossibleUnhealthy(vouchId);
    _vouchShouldBelongToAuthor(vouchId, profileId);
    v.unhealthy = true;
    // solhint-disable-next-line not-rely-on-time
    v.activityCheckpoints.unhealthyAt = block.timestamp;

    emit MarkedUnhealthy(v.vouchId, v.authorProfileId, v.subjectProfileId);
  }
```
```solidity
  function _vouchShouldBePossibleUnhealthy(uint256 vouchId) private view {
    Vouch storage v = vouches[vouchId];
    bool stillHasTime = block.timestamp <=
      v.activityCheckpoints.unvouchedAt + unhealthyResponsePeriod;

    if (!v.archived || v.unhealthy || !stillHasTime) {
      revert CannotMarkVouchAsUnhealthy(vouchId);
    }
  }
```
However, if `unhealthyResponsePeriod` is updated to a smaller value, some authors might be unable to mark their unvouched vouch as `unhealthy`:
- Suppose `unhealthyResponsePeriod` is `24 hours`
- Alice unvouches one of her vouch at time `T`. She can mark it as `unhealthy` before `T + 24 hours`
- `unhealthyResponsePeriod` is updated to `8 hours` at `T + 12 hours`
- Alice is unable to mark her vouch as `unhealthy` due to expiration.

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The author might be unable to mark their unvouched vouch as `unhealthy` in time if `unhealthyResponsePeriod` is updated suddenly
### PoC

_No response_

### Mitigation

Each vouch should have their own `unhealthyResponseEndTime`:
```diff
  struct ActivityCheckpoints {
    uint256 vouchedAt;
    uint256 unvouchedAt;
    uint256 unhealthyAt;
+   uint256 unhealthyResponseEndTime;
  }

  function unvouch(uint256 vouchId) public whenNotPaused nonReentrant {
    Vouch storage v = vouches[vouchId];
    _vouchShouldExist(vouchId);
    _vouchShouldBePossibleUnvouch(vouchId);
    // because it's $$$, you can only withdraw/unvouch to the same address you used to vouch
    // however, we don't care about the status of the address's profile; funds are always attached
    // to an address, not a profile
    if (vouches[vouchId].authorAddress != msg.sender) {
      revert AddressNotVouchAuthor(vouchId, msg.sender, vouches[vouchId].authorAddress);
    }

    v.archived = true;
    // solhint-disable-next-line not-rely-on-time
    v.activityCheckpoints.unvouchedAt = block.timestamp;
+   v.activityCheckpoints.unhealthyResponseEndTime = block.timestamp + unhealthyResponsePeriod;
    // remove the vouch from the tracking arrays and index mappings
    _removeVouchFromArrays(v);

    // apply fees and determine how much is left to send back to the author
    (uint256 toWithdraw, ) = applyFees(v.balance, false, v.subjectProfileId);
    // set the balance to 0 and save back to storage
    v.balance = 0;
    // send the funds to the author
    // note: it sends it to the same address that vouched; not the one that called unvouch
    (bool success, ) = payable(v.authorAddress).call{ value: toWithdraw }("");
    if (!success) {
      revert FeeTransferFailed("Failed to send ETH to author");
    }

    emit Unvouched(v.vouchId, v.authorProfileId, v.subjectProfileId);
  }

  function _vouchShouldBePossibleUnhealthy(uint256 vouchId) private view {
    Vouch storage v = vouches[vouchId];
-   bool stillHasTime = block.timestamp <=
-     v.activityCheckpoints.unvouchedAt + unhealthyResponsePeriod;
+   bool stillHasTime = block.timestamp <=
+     v.activityCheckpoints.unhealthyResponseEndTime;

    if (!v.archived || v.unhealthy || !stillHasTime) {
      revert CannotMarkVouchAsUnhealthy(vouchId);
    }
  }
```