Tall Cream Finch

High

# The fees should not be added to the `marketFunds` in the `buyVotes` function.

### Summary


In [`ReputationMarket.sol:481`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481), `fundsPaid` is added to `marketFunds` to update the funds currently invested in the market. However, `fundsPaid` includes the buying fees, which should not be added to `marketFunds`. Because these fees have already been allocated to `protocolFeeAddress` and `donationRecipient` in `ReputationMarket.sol:464`, they no longer belong to the market. This results in `marketFunds` being larger than the actual funds, potentially causing the protocol to lose funds during the `withdrawGraduatedMarketFunds`.

When buying votes, the actual ETH amount need to be paid (i.e. `fundsPaid`) is calculated in `_calculateBuy`. According to [`ReputationMarket.sol:978`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L978), `fundsPaid` includs the buying fees (`protocolFee` and `donation`).
```solidity
// function: ReputationMarket.sol:_calculateBuy()

978:    fundsPaid += protocolFee + donation;

        maxPrice = votePrice;

        return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
```
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L978-L982


Then in [`ReputationMarket.sol:464`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L464), the fees are applied, `protocolFee` is transferred to `protocolFeeAddress`, and `donation` is allocated to `donationRecipient`. This means that these fees do not belong to the market. However, in [`ReputationMarket.sol:481`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481), these fees are also added to `marketFunds` because they are included in `fundsPaid`.
```solidity
// function: ReputationMarket.sol:buyVotes()

        // Determine how many votes can be bought with the funds provided
        (
          uint256 votesBought,
453:      uint256 fundsPaid,
          ,
          uint256 protocolFee,
          uint256 donation,
          uint256 minVotePrice,
          uint256 maxVotePrice
        ) = _calculateBuy(markets[profileId], isPositive, msg.value);

        _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

        // Apply fees first
464:    applyFees(protocolFee, donation, profileId);

        // Update market state
        markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
        votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

        // Add buyer to participants if not already a participant
        if (!isParticipant[profileId][msg.sender]) {
          participants[profileId].push(msg.sender);
          isParticipant[profileId][msg.sender] = true;
        }

        // Calculate and refund remaining funds
        uint256 refund = msg.value - fundsPaid;
        if (refund > 0) _sendEth(refund);

        // tally market funds
481:    marketFunds[profileId] += fundsPaid;
```
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L450-L481

This lead to issues during the withdrawal process from graduated markets in [`ReputationMarket.sol:675`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L675):
1. If the balance is insufficient, the `_sendEth` will be reverted.
2. If the balance is sufficient, the `_sendEth` will cause `ReputationMarket.sol` to lose funds.
This is because the amount of ETH transferred out to GRADUATION_WITHDRAWAL address is greater than the amount transferred in during buying.
```solidity
// function: ReputationMarket.sol:withdrawGraduatedMarketFunds()

675:    _sendEth(marketFunds[profileId]);  
```
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L675

### Root Cause

In [`ReputationMarket.sol:481`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481), buying fees are also added to `marketFunds`, which makes `marketFunds` greater than the actual funds, thereby possibly causing the transaction to revert or resulting in the protocol losing funds when withdrawing funds from the graduated market.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. A market is created.
2. Alice buys votes for the market.
3. GraduateContract graduates the market.
4. GraduateContract withdraws funds from the market, and the withdrawal reverts.

### Impact

This issue causes the transaction to revert or results in the protocol losing funds when withdrawing funds from the graduated market.

### PoC

_No response_

### Mitigation

```solidity
// function: ReputationMarket.sol:buyVotes()

-    marketFunds[profileId] += fundsPaid;
+    marketFunds[profileId] += fundsPaid - protocolFee - donation;
```