Hidden Blonde Mustang

Medium

# # Invalid `marketFunds[profileId]`(buyVotes())


### Summary
`marketFunds[profileId]`s calculation is incorrect.

### Root Cause
In the `buyVotes()` function, `marketFunds[profileId]` is increased to include the fees and donations.
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L481

### Internal pre-conditions
N/A

### External pre-conditions
N/A

### Attack Path
N/A

### Impact
The value of `marketFunds[profileId]` is increased to include protocol fees and donations that have already been paid to the protocol and the market owner. 
As a result, `marketFunds[profileId]` is greater than actual funds currently invested in this market.
Consequently, the `withdrawGraduatedMarketFunds()` function will withdraw more than intended, which may prevent others from being able to withdraw their funds.
Additionally, votes may not be sold due to insufficient funds.

### PoC
```solidity
ReputationMarket.sol
660:    function withdrawGraduatedMarketFunds(uint256 profileId) public whenNotPaused {
            address authorizedAddress = contractAddressManager.getContractAddressForName(
                "GRADUATION_WITHDRAWAL"
            );
            if (msg.sender != authorizedAddress) {
                revert UnauthorizedWithdrawal();
            }
            _checkMarketExists(profileId);
            if (!graduatedMarkets[profileId]) {
                revert MarketNotGraduated();
            }
            if (marketFunds[profileId] == 0) {
                revert InsufficientFunds();
            }

675:        _sendEth(marketFunds[profileId]);
            emit MarketFundsWithdrawn(profileId, msg.sender, marketFunds[profileId]);
            marketFunds[profileId] = 0;
        }

442:    function buyVotes(
            uint256 profileId,
            bool isPositive,
            uint256 expectedVotes,
            uint256 slippageBasisPoints
        ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
            _checkMarketExists(profileId);

            // Determine how many votes can be bought with the funds provided
            (
            uint256 votesBought,
            uint256 fundsPaid,
            ,
            uint256 protocolFee,
            uint256 donation,
            uint256 minVotePrice,
            uint256 maxVotePrice
            ) = _calculateBuy(markets[profileId], isPositive, msg.value);

            _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

            // Apply fees first
464:        applyFees(protocolFee, donation, profileId);

            // Update market state
            markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
            votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

            // Add buyer to participants if not already a participant
            if (!isParticipant[profileId][msg.sender]) {
                participants[profileId].push(msg.sender);
                isParticipant[profileId][msg.sender] = true;
            }

            // Calculate and refund remaining funds
477:        uint256 refund = msg.value - fundsPaid;
            if (refund > 0) _sendEth(refund);

            // tally market funds
481:        marketFunds[profileId] += fundsPaid;
            [...]
        }

1166:   function applyFees(
            uint256 protocolFee,
            uint256 donation,
            uint256 marketOwnerProfileId
        ) private returns (uint256 fees) {
1121:       donationEscrow[donationRecipient[marketOwnerProfileId]] += donation;
            if (protocolFee > 0) {
1123:         (bool success, ) = protocolFeeAddress.call{ value: protocolFee }("");
              if (!success) revert FeeTransferFailed("Protocol fee deposit failed");
            }
            fees = protocolFee + donation;
        }
```
In the `buyVotes()` function, `marketFunds[profileId]` is increased by all funds sent by the buyer, including protocol fees and donations.

### Mitigation
```solidity
ReputationMarket.sol
442:    function buyVotes(
            uint256 profileId,
            bool isPositive,
            uint256 expectedVotes,
            uint256 slippageBasisPoints
        ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
            [...]
464:        applyFees(protocolFee, donation, profileId);

            // Update market state
            markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
            votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

            // Add buyer to participants if not already a participant
            if (!isParticipant[profileId][msg.sender]) {
            participants[profileId].push(msg.sender);
            isParticipant[profileId][msg.sender] = true;
            }

            // Calculate and refund remaining funds
477:        uint256 refund = msg.value - fundsPaid;
            if (refund > 0) _sendEth(refund);

            // tally market funds
-           marketFunds[profileId] += fundsPaid;
+           marketFunds[profileId] += fundsPaid - protocolFee - donation;
            [...]
        }
```