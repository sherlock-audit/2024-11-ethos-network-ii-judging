Custom Foggy Pike

Medium

# `ReputationMarket.buyVotes()` adds the protocol and donation fees to the `marketFunds` which would result in draining the contract funds when a market is graduated

### Summary

`ReputationMarket.buyVotes()` adds the fees paid for the protocol and the donation fees to the `marketFunds` of a market instead of adding the votes price only, which would result in draining the contract funds when a market is graduated and its funds are withdrawn via `ReputationMarket.withdrawGraduatedMarketFunds()`, as the graduation admin will receive the saved `marketFunds` of the market which is more than the actual market funds by the amount of protocol fees and donation fees calculated for each votes purchase.

### Root Cause

`ReputationMarket.buyVotes()` [incorrectly](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L481) adds the fees paid for the protocol and the donation fees to the `marketFunds` of a market instead of adding the votes price only:

```javascript
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
 // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first

    applyFees(protocolFee, donation, profileId);
    //...

    // tally market funds
     // @audit-issue : `applyFees()` will SEND the fees for the protocol treasury and the market owner, so these fees are paid and shouldn't be counted in the marketFunds
    marketFunds[profileId] += fundsPaid;
    //...
  }
```

### Internal pre-conditions

- Users call `ReputationMarket.buyVotes()` to buy trust/distrust votes from a market, where they determine the minimum amount of votes they wish to receive along with an amount of native tokens (`msg.value`):

```javascript
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
 // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first

    applyFees(protocolFee, donation, profileId);
    //...

    // tally market funds
    marketFunds[profileId] += fundsPaid;
    //...
  }
```

- [`_calculateBuy()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L978) function **returns the `fundsPaid` that represents the price of votes + protocol fees + donation fees**, where these fees are sent to the intended destinations via [`applyFees()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1116C3-L1127C4).

```javascript
function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
    fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }
```

- So for each purchase, there's:
  - a protocol fee that is calculated as a percentage of the `msg.value` (via `_calculateBuy()`) and sent to the protocol treasury via [`applyFees()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1116C3-L1127C4).
  - a donation fee that is calculated as a percentage of the `msg.value` (via `_calculateBuy()`) and sent to the market's donation recipient via [`applyFees()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1116C3-L1127C4).

```javascript
function applyFees(
    uint256 protocolFee,
    uint256 donation,
    uint256 marketOwnerProfileId
  ) private returns (uint256 fees) {
    donationEscrow[donationRecipient[marketOwnerProfileId]] += donation;
    if (protocolFee > 0) {
      (bool success, ) = protocolFeeAddress.call{ value: protocolFee }("");
      if (!success) revert FeeTransferFailed("Protocol fee deposit failed");
    }
    fees = protocolFee + donation;
  }
```

- After these fees are calculated and sent to the intended parties, the remaining amount of the `fundsPaid` **should** represent the price of the bought votes, and this value should be assigned to the `marketFunds[profileId]`, but it was noticed that the total amount of the `fundsPaid` is assigned to the `marketFunds[profileId]`, which is incorrect **as the protocol fees and donation fees are sent out and should be deducted from the `fundsPaid` before adding it to the marketFunds**:

```javascript
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
 // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first

    applyFees(protocolFee, donation, profileId);
    //...

    // tally market funds
     // @audit-issue : `applyFees()` will SEND the fees for the protocol treasury and the market owner, so these fees are paid and shouldn't be counted in the marketFunds
    marketFunds[profileId] += fundsPaid;
    //...
  }
```

- This will result in saving a marketFunds larger than the actual received market funds, which will create an issue when a market is graduated, as [the saved `marketFunds`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L675) will be sent to the graduation admin , resulting in draining the funds for other markets, hence draining the contract funds with each graduated market:

```javascript
function withdrawGraduatedMarketFunds(uint256 profileId) public whenNotPaused {
    //...
    _sendEth(marketFunds[profileId]);
    emit MarketFundsWithdrawn(profileId, msg.sender, marketFunds[profileId]);
    marketFunds[profileId] = 0;
  }
```


### External pre-conditions

_No response_

### Attack Path

1. Users buys votes from a market, the `marketFunds[profileId]` saves the **total amount paid by the buyers** without deducting the protocol and donation fees that have been sent out.
2. A market is graduated (marked as inactive for trading).
3. The graduation admin calls `withdrawGraduatedMarketFunds()` to withdraw the funds from the graduated market, but the funds sent out to him will be larger than the actual marketFunds (larger than the bought votes price by the protocol and donation fees calaculated for each purchase).

### Impact

The locked native tokens in the contract are less than the recorded total funds for all markets, and by graduating each market and withdrawing its funds; the contract funds are drained until it reaches a state where the remaining active markets funds are insufficient to pay for selling votes as the contract balance is less than recorded (the sum of the `marketFunds` of all active markets).

### PoC

_No response_

### Mitigation

Deduct the protocol and donation fees from the `fundsPaid` before adding it to the market funds:

```diff
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
 // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first

    applyFees(protocolFee, donation, profileId);
    //...

    // tally market funds
-   marketFunds[profileId] += fundsPaid;
+   marketFunds[profileId] += fundsPaid - protocolFee - donation;
    //...
  }
```