Fantastic Paisley Jay

High

# The funds assigned to the `marketFunds[profileId]` in `ReputationMarket::buyVotes` are incorrect

### Summary

The `ReputationMarket::buyVotes` function is used for buying a votes for a given market. The function applies fees and updates `marketFunds` mapping. But the `marketFunds` mapping is updated with the incorrect value. 

### Root Cause

The `buyVotes` function allows a user to buy votes. Let's consider the function calls that this function makes. The important for us will be: `_calculateBuy` and `applyFees`:

```solidity

function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
@>  ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);

    // Apply fees first
@>  applyFees(protocolFee, donation, profileId);

    // Update market state
    markets[profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;
    votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST] += votesBought;

    // Add buyer to participants if not already a participant
    if (!isParticipant[profileId][msg.sender]) {
      participants[profileId].push(msg.sender);
      isParticipant[profileId][msg.sender] = true;
    }

    // Calculate and refund remaining funds
    uint256 refund = msg.value - fundsPaid;
    if (refund > 0) _sendEth(refund);

    // tally market funds
@>  marketFunds[profileId] += fundsPaid;
    emit VotesBought(
      profileId,
      msg.sender,
      isPositive,
      votesBought,
      fundsPaid,
      block.timestamp,
      minVotePrice,
      maxVotePrice
    );
    _emitMarketUpdate(profileId);
}

```
The `_calculateBuy` function calculates the outcome of a buy transaction. The return value `fundsPaid` represents the total amount that is paid including fees:

```solidity

function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);
    
    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
@>    fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
@>  fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }

```
Then the `buyVotes` function calls the `applyFees` function with arguments `protocolFee`, `donation` and `profileId`. The `applyFees` function processes protocol fees and donations for a market transaction. The function adds the `donation` to the `donationEscrow` mapping and send the `protocolFee` to the `protocolFeeAddress`:

```solidity

function applyFees(
    uint256 protocolFee,
    uint256 donation,
    uint256 marketOwnerProfileId
  ) private returns (uint256 fees) {
@>  donationEscrow[donationRecipient[marketOwnerProfileId]] += donation;
    if (protocolFee > 0) {
@>    (bool success, ) = protocolFeeAddress.call{ value: protocolFee }("");
      if (!success) revert FeeTransferFailed("Protocol fee deposit failed");
    }
    fees = protocolFee + donation;
}

```

The problem is that the `buyVotes` function adds the `fundsPaid` to the `marketFunds[profileId]`. As seen from the `_calculateBuy` function, the `fundsPaid` includes the value of `donation`, `protocolFee` and funds paid for the votes. 
This is a problem because the `applyFees` function already sent the value of `protocolFee` to the `protocolFeeAddress` and updated the `donationEscrow` mapping. This means when the user calls the [`withdrawDonations`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L570-L585) function, the user will withdraw the `donation` amount that is added to the `marketFunds` mapping. And when the [`withdrawGraduatedMarketFunds`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L660-L678) function is called, the function will revert due to insufficient funds in the contract. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The `withdrawGraduatedMarketFunds` function will revert due to insufficient funds, as the funds to be sent have already been spent.
Likelihood is High and the Impact is High.

### PoC

_No response_

### Mitigation

Don't update the `marketFunds[profileId]` mapping with the whole `fundsPaid` amount in `buyVotes`. It should be updated with only the value that is paid for the votes:

```diff
- marketFunds[profileId] += fundsPaid;
+ marketFunds[profileId] += fundsPaid - donation - protocolFee;
```