Festive Rosewood Porpoise

High

# The funds of the ReputationMarket contract can be drained due to incorrect accounting of fees.

### Summary

In the ReputationMarket contract, there is mechanism for buying and selling the votes at reputation market and various fees are applied for every transaction. After buying and selling the votes, it looks like the market has more funds than real, since various fees are incorrectly accounted. Therefore, when withdraw funds of the market, they can withdraw more funds than real and the funds of the contract can be drained.

### Root Cause

In the [ReputationMarket.sol:L442-L493](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L442-L493) `buyVotes()` function, we calculate how many votes can be bought with the funds provided and updates state variable `marketFunds`. 

```solidity
    function buyVotes(
        uint256 profileId,
        bool isPositive,
        uint256 expectedVotes,
        uint256 slippageBasisPoints
    ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
        _checkMarketExists(profileId);

        // Determine how many votes can be bought with the funds provided
        (
        uint256 votesBought,
        uint256 fundsPaid,
        ,
        uint256 protocolFee,
        uint256 donation,
        uint256 minVotePrice,
        uint256 maxVotePrice
    @>  ) = _calculateBuy(markets[profileId], isPositive, msg.value);

        ...
        // tally market funds
    @>  marketFunds[profileId] += fundsPaid;
        ...
    }
```

Let's consider `fundsPaid` which is added to the funds of market. The calculation of `fundsPaid` is in the [ReputationMarket.sol:L942-L983](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L942-L983) `ReputationMarket._calculateBuy()` function. In this function, we calculate the `protocolFee` and `donation` which is going to be sent and add fees to `fundsPaid`. 

Here is issue. The real amount of funds that the market will receive should not include the fees, since the fees are sent to protocol and donationRecipient. However, the function `ReputationMarket.buyVotes()` adds funds including the fees to its account. Therefore, state variable `marketFunds` stores more funds than real and it will withdraw more funds from contract. As a result, the funds of the contract will be drained.

```solidity
    function _calculateBuy(
        Market memory market,
        bool isPositive,
        uint256 funds
    )
        private
        view
        returns (
        uint256 votesBought,
        uint256 fundsPaid,
        uint256 newVotePrice,
        uint256 protocolFee,
        uint256 donation,
        uint256 minVotePrice,
        uint256 maxVotePrice
        )
    {
        uint256 fundsAvailable;
@>      (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
        uint256 votePrice = _calcVotePrice(market, isPositive);

        uint256 minPrice = votePrice;
        uint256 maxPrice;

        if (fundsAvailable < votePrice) {
        revert InsufficientFunds();
        }

        while (fundsAvailable >= votePrice) {
        fundsAvailable -= votePrice;
        fundsPaid += votePrice;
        votesBought++;

        market.votes[isPositive ? TRUST : DISTRUST] += 1;
        votePrice = _calcVotePrice(market, isPositive);
        }
@>      fundsPaid += protocolFee + donation;

        maxPrice = votePrice;

        return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
    }
```

The incorrect accounting of the fees is also included in the [ReputationMarket.sol:L495-L534](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L495-L534) `sellVotes()` function and [ReputationMarket.sol:L1003-L1045](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/ReputationMarket.sol#L1003-L1045)  `_calculateSell()` function.

```solidity
    function sellVotes(
        uint256 profileId,
        bool isPositive,
        uint256 amount
    ) public whenNotPaused activeMarket(profileId) nonReentrant {
        _checkMarketExists(profileId);

        // calculate the amount of votes to sell and the funds received
        (
        uint256 votesSold,
        uint256 fundsReceived,
        ,
        uint256 protocolFee,
        uint256 minVotePrice,
        uint256 maxVotePrice
@>      ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

        ...
        // tally market funds
@>      marketFunds[profileId] -= fundsReceived;
    ...
```

```solidity
    function _calculateSell(
        Market memory market,
        uint256 profileId,
        bool isPositive,
        uint256 amount
    )
        private
        view
        returns (
        uint256 votesSold,
        uint256 fundsReceived,
        uint256 newVotePrice,
        uint256 protocolFee,
        uint256 minVotePrice,
        uint256 maxVotePrice
        )
    {
        uint256 votesAvailable = votesOwned[msg.sender][profileId].votes[isPositive ? TRUST : DISTRUST];

        if (votesAvailable < amount) {
        revert InsufficientVotesOwned(profileId, msg.sender);
        }

        uint256 votePrice = _calcVotePrice(market, isPositive);

        uint256 maxPrice = votePrice;
        uint256 minPrice;

        while (votesSold < amount) {
            if (market.votes[isPositive ? TRUST : DISTRUST] <= 1) {
                revert InsufficientVotesToSell(profileId);
            }

            market.votes[isPositive ? TRUST : DISTRUST] -= 1;
            votePrice = _calcVotePrice(market, isPositive);
            fundsReceived += votePrice;
            votesSold++;
        }
@>      (fundsReceived, protocolFee, ) = previewFees(fundsReceived, false);
        minPrice = votePrice;

        return (votesSold, fundsReceived, votePrice, protocolFee, minPrice, maxPrice);
    }
```

In the `ReputationMarket.sellVotes()` function, we subtract `fundsReceived` from original funds of market. However, `fundsReceived` doesn't include fee which is sent to protocol from market. The `protocolFee` should be sent from the funds of market. Therefore, the funds of market looks like more than real, since the `protocolFee` is not included. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The funds of the ReputationMarket contract will be drained due to incorrect accounting of fees.

### PoC

_No response_

### Mitigation

Implement correct accounting of fees to the functions.

```diff
    function buyVotes(
        uint256 profileId,
        bool isPositive,
        uint256 expectedVotes,
        uint256 slippageBasisPoints
    ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
        _checkMarketExists(profileId);

        // Determine how many votes can be bought with the funds provided
        (
        uint256 votesBought,
        uint256 fundsPaid,
        ,
        uint256 protocolFee,
        uint256 donation,
        uint256 minVotePrice,
        uint256 maxVotePrice
        ) = _calculateBuy(markets[profileId], isPositive, msg.value);

        ...
        // tally market funds
+       fundsPaid -= protocolFee;
+       fundsPaid -= donation;
        marketFunds[profileId] += fundsPaid;
        ...
    }
```

```diff
    function sellVotes(
        uint256 profileId,
        bool isPositive,
        uint256 amount
    ) public whenNotPaused activeMarket(profileId) nonReentrant {
        _checkMarketExists(profileId);

        // calculate the amount of votes to sell and the funds received
        (
        uint256 votesSold,
        uint256 fundsReceived,
        ,
        uint256 protocolFee,
        uint256 minVotePrice,
        uint256 maxVotePrice
        ) = _calculateSell(markets[profileId], profileId, isPositive, amount);

        ...
+       fundsReceived += protocolFee
        // tally market funds
        marketFunds[profileId] -= fundsReceived;
    ...
```