Striped Fuchsia Fly

Medium

# Vote buyers will have to pay more fees than expected

### Summary

Taking fees from total input amount will cause the vote buyers to spend more fees than expected

### Root Cause

- In [function `ReputationMarket::_calculateBuy()`](https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/main/ethos/packages/contracts/contracts/ReputationMarket.sol#L942-L983), the fees are taken from the buyer input amount
```solidity
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);

    // Determine how many votes can be bought with the funds provided
    (
      uint256 votesBought,
      uint256 fundsPaid,
      ,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
@>    ) = _calculateBuy(markets[profileId], isPositive, msg.value);

    _checkSlippageLimit(votesBought, expectedVotes, slippageBasisPoints);
    ...
}
```
- The amount after fee `fundsAvailable` is then used to calculate votes bought by the user. The final amount the buyer needs to pay is `fundsPaid`, which finally includes the fees above. Note that `fundsPaid` is the effective capital.
```solidity
  function _calculateBuy(
    Market memory market,
    bool isPositive,
    uint256 funds
  )
    private
    view
    returns (
      uint256 votesBought,
      uint256 fundsPaid,
      uint256 newVotePrice,
      uint256 protocolFee,
      uint256 donation,
      uint256 minVotePrice,
      uint256 maxVotePrice
    )
  {
    uint256 fundsAvailable;
@>    (fundsAvailable, protocolFee, donation) = previewFees(funds, true);
    uint256 votePrice = _calcVotePrice(market, isPositive);

    uint256 minPrice = votePrice;
    uint256 maxPrice;

    if (fundsAvailable < votePrice) {
      revert InsufficientFunds();
    }

    while (fundsAvailable >= votePrice) {
      fundsAvailable -= votePrice;
      fundsPaid += votePrice;
      votesBought++;

      market.votes[isPositive ? TRUST : DISTRUST] += 1;
      votePrice = _calcVotePrice(market, isPositive);
    }
@>    fundsPaid += protocolFee + donation;

    maxPrice = votePrice;

    return (votesBought, fundsPaid, votePrice, protocolFee, donation, minPrice, maxPrice);
  }

  function previewFees(
    uint256 amount,
    bool isEntry
  ) private view returns (uint256 funds, uint256 protocolFee, uint256 donation) {
    if (isEntry) {
@>      protocolFee = (amount * entryProtocolFeeBasisPoints) / BASIS_POINTS_BASE;
@>      donation = (amount * donationBasisPoints) / BASIS_POINTS_BASE;
    } else {
      protocolFee = (amount * exitProtocolFeeBasisPoints) / BASIS_POINTS_BASE;
    }
    funds = amount - protocolFee - donation;
  }
```

- The leftover amount `msg.value - fundsPaid` is then refunded to the buyer
```solidity
  function buyVotes(
    uint256 profileId,
    bool isPositive,
    uint256 expectedVotes,
    uint256 slippageBasisPoints
  ) public payable whenNotPaused activeMarket(profileId) nonReentrant {
    _checkMarketExists(profileId);
...
    // Calculate and refund remaining funds
@>    uint256 refund = msg.value - fundsPaid;
@>    if (refund > 0) _sendEth(refund);
...
```

The buyer's input amount can be higher than the effective amount that is used to actually buy votes. So fees taken is slightly higher than expected in each buy transaction. The difference is more clear when observing in many buy transactions, such that when an users buy votes using `1 ether` will spend less fee than buy votes using 2 transactions with `0.5 ether`.

- Combined with the design that using `while` loop in function `_calculateBuy()`, it is more likely that users who want to buy with a large amount of funds will have to submit many `buyVotes()` transactions. This cause the users have to spend fees overhead

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

- Users have to pay fee for the ineffective capital, resulting protocol takes more profit
- Users have to pay more fees to buy votes with large amount of funds than expected because they have to divide the funds into many transactions

### PoC

Prepare a Foundry test as below:
```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {EthosVouch} from "contracts/EthosVouch.sol";
import {ReputationMarket} from "contracts/ReputationMarket.sol";

contract MyTest is Test {
    EthosVouch vouch;
    ReputationMarket market;

    address THIS = address(this);

    mapping(address => uint256) id;

    address buyer;

    function setUp() public {
        vouch = new EthosVouch();

        vm.store(address(vouch), 0xf0c57e16840df040f15088dc2f81fe391c3923bec73e23a9662efc9c229c6a00, bytes32(0));

        vouch.initialize(THIS, THIS, THIS, THIS, THIS, THIS, 0, 0, 0, 0);

        market = new ReputationMarket();

        vm.store(address(market), 0xf0c57e16840df040f15088dc2f81fe391c3923bec73e23a9662efc9c229c6a00, bytes32(0));

        market.initialize(THIS, THIS, THIS, THIS, THIS);

        id[THIS] = 1;

        buyer = makeAddr("buyer");
        deal(buyer, 100 ether);
        id[buyer] = 2;
    }

    function test_IneffectiveFee() public {
        // create market and set fees
        market.createMarketWithConfigAdmin{value: 0.01 ether * 50}(THIS, 1);
        market.setProtocolFeeAddress(THIS);
        market.setEntryProtocolFeeBasisPoints(500);
        market.setDonationBasisPoints(500);
        market.setExitProtocolFeeBasisPoints(500);

        vm.startPrank(buyer);
        
        uint256 snapshotId = vm.snapshot();
        uint256 buyAmount = 0.1 ether;

        // User buys with 1 transaction
        market.buyVotes{value: buyAmount}(1, true, 0, 0);

        uint256 buyerBalanceAfter1 = address(buyer).balance;

        ReputationMarket.MarketInfo memory userVotes1 = market.getUserVotes(buyer, 1);

        vm.revertTo(snapshotId);
        
        (, uint256 fundsPaid,,,,,) = market.simulateBuy(1, true, buyAmount / 2);
        
        // User buys with 2 transactions
        market.buyVotes{value: buyAmount / 2}(1, true, 0, 0);

        market.buyVotes{value: buyAmount - fundsPaid}(1, true, 0, 0);

        uint256 buyerBalanceAfter2 = address(buyer).balance;

        ReputationMarket.MarketInfo memory userVotes2 = market.getUserVotes(buyer, 1);


        // resulting with same votes amount in 2 cases
        assertEq(userVotes1.trustVotes, userVotes2.trustVotes, "votes");

        // check balance in 2 cases
        assertEq(buyerBalanceAfter1, buyerBalanceAfter2, "balances");
    }

    function getContractAddressForName(string memory) public returns (address) {
        return THIS;
    }

    function verifiedProfileIdForAddress(address input) public returns (uint256) {
        return id[input];
    }

    fallback() external payable {}
}
```

Run the test and console shows:
```bash
Failing tests:
Encountered 1 failing test in test/MyTest.t.sol:MyTest
[FAIL: balances: 99904661858515683084 != 99904168841064537412] test_IneffectiveFee() (gas: 785520)
```
The difference is `99904661858515683084 - 99904168841064537412 = 493017451145672`, which approx 0.49% compared to the buy amount `0.1 ether`

### Mitigation

Consider updating the fee mechanism to only take fee for the effective funds